/* EE dialogues */

/* Ajantis about Neera or another wildmage - PC is no wildmage */

/* dialogue activation */
IF	%BGT_VAR% 
	InParty(Myself)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	%InMyArea_Player1%
	!See([ENEMY])
	%CombatCounter_0%
	Global("C#AjantisBG1_AboutWildmages","GLOBAL",0)
	!Kit(Player1,WILDMAGE)
	OR(6)
		InParty("neera") 
		Kit(Player2,WILDMAGE) 
		Kit(Player3,WILDMAGE) 
		Kit(Player4,WILDMAGE) 
		Kit(Player5,WILDMAGE) 
		Kit(Player6,WILDMAGE)
THEN
  RESPONSE #100
  	SetGlobal("C#AjantisBG1_AboutWildmages","GLOBAL",1)
END

/* Dialogue goes a bit differently if CHARNAME is a wildmage */
IF	%BGT_VAR% 
	InParty(Myself)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	%InMyArea_Player1%
	!See([ENEMY])
	%CombatCounter_0%
	Global("C#AjantisBG1_AboutWildmages","GLOBAL",0)
	Kit(Player1,WILDMAGE)
	InParty("neera")
THEN
  RESPONSE #100
  	SetGlobal("C#AjantisBG1_AboutWildmages","GLOBAL",1)
END


/* dialogue triggering */
IF 	%BGT_VAR%
	InParty(Myself)
	%InMyArea_Player1%
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	Global("C#AjantisBG1_AboutWildmages","GLOBAL",1)
THEN
  RESPONSE #100
	
	%SET_FP_TIMER%
	%STARTDIALOGNOSET_PLAYER1%
END

/* Ajantis talks about Rasaad */
/* dialogue activation */
IF	%BGT_VAR% 
//	%FP_TIMER_EXPIRED%
	GlobalGT("RASAAD_ROMANCE","GLOBAL",0)
	OR(2)
		Global("X#AjantisRomanceActive","GLOBAL",1)
		Global("X#AjantisRomanceActive","GLOBAL",2)
	InParty("rasaad")
	InParty(Myself)
	See(Player1)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	!StateCheck(Myself,CD_STATE_NOTVALID) 
	CombatCounter(0) 
	!See([ENEMY])
	Global("C#AjantisBG1_RasaadRomConflictPC","GLOBAL",0)
THEN
  RESPONSE #100
  	SetGlobal("C#AjantisBG1_RasaadRomConflictPC","GLOBAL",1)
END

/* dialogue triggering */
IF 	%BGT_VAR%
	InParty(Myself)
	%InMyArea_Player1%
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	Global("C#AjantisBG1_RasaadRomConflictPC","GLOBAL",1)
THEN
  RESPONSE #100
	
	%SET_FP_TIMER%
	%STARTDIALOGNOSET_PLAYER1%
END