
//Ajantis BG1 expansion
//original romance was developped for BGTutu / BGT "BG1NPC project", G3
// Author: jastey


//original BG1:TotSC, BGT, BG:EE, EET. Automatic game detection upon install.

//For BG1:TotSC, this mod adds the BG1NPC Ajantis romance, and a friendship path
//For BGT, BG:EE, and EET this mod adds the Ajantis friendship path and some more romance reactions in addition to the BG1NPC Project

BACKUP ~AjantisBG1/Backup~
AUTHOR ~Please post at G3 or Kerzenburgforum, refer to readme.~

//MODDER

VERSION ~22pre~ //

README ~AjantisBG1/docs/readme.ajantisbg1.%LANGUAGE%.txt~ ~AjantisBG1/docs/readme.ajantisbg1.english.txt~


ALWAYS

/* check for a ready SoD */

 ACTION_IF ((FILE_EXISTS ~dlc/sod-dlc.zip~) OR (FILE_EXISTS ~sod-dlc.zip~)) THEN BEGIN
  FAIL ~Your SoD game needs to be modmerged before mods can be installed on this game. Check the readme for more information and a link to download the DLC Merger.~
 END

 ACTION_IF GAME_IS ~tutu tutu_totsc~ THEN BEGIN
    INCLUDE ~AjantisBG1/lib/ajantis_tutu.tpa~
    INCLUDE ~AjantisBG1/lib/g3_tutu_cpmvars.tpa~

    /* Tell the player it is using Tutu */
    PRINT @1
 END 

 ACTION_IF GAME_IS ~bgt~ THEN BEGIN
    INCLUDE ~AjantisBG1/lib/ajantis_bgt.tpa~
    INCLUDE ~AjantisBG1/lib/g3_bgt_cpmvars.tpa~

     /* Tell the player it is using BGT stuff */
      PRINT @2
 END 

 ACTION_IF GAME_IS ~bgee~ THEN BEGIN
   INCLUDE ~AjantisBG1/lib/ajantis_bgee.tpa~
   INCLUDE ~AjantisBG1/lib/g3_bgee_cpmvars.tpa~
  OUTER_SPRINT ~46137~ ~46137~
  OUTER_SPRINT ~46138~ ~46138~
  OUTER_SPRINT ~46139~ ~46139~
  OUTER_SPRINT ~46140~ ~46140~
  OUTER_SPRINT ~46145~ ~46145~
        PRINT @14
 END

 ACTION_IF GAME_IS ~eet~ THEN BEGIN
   INCLUDE ~AjantisBG1/lib/ajantis_eet.tpa~
      INCLUDE ~EET/other/cpmvars/eet_cpmvars.tpa~
  OUTER_SPRINT ~46137~ ~246137~
  OUTER_SPRINT ~46138~ ~246138~
  OUTER_SPRINT ~46139~ ~246139~
  OUTER_SPRINT ~46140~ ~246140~
  OUTER_SPRINT ~46145~ ~246145~
        PRINT @15
 END

/* (T3) Define a variable to account for the differences in string ref numbers between BG:SoD and EET. - Works for string references in d- or baf-files. */
  ACTION_IF GAME_IS ~bgee~ BEGIN
    OUTER_SPRINT ~2~ ~~
  END

  ACTION_IF GAME_IS ~eet~ BEGIN
    OUTER_SPRINT ~2~ ~2~
  END

/* (T1) Definition of variables to distinguish between SoD and BG1 (for banters or script blocks) */
  ACTION_IF (GAME_IS ~bgee~) THEN BEGIN
    ACTION_IF (MOD_IS_INSTALLED ~c#endlessbg1.tp2~ ~14~) BEGIN
      OUTER_SPRINT ~BG1_BEFORE_TRANSITION~ ~GlobalLT("BD_PLOT","GLOBAL",41)~
      OUTER_SPRINT ~IT_IS_SOD~ ~GlobalGT("bd_plot","global",40)~
    END ELSE ACTION_IF (MOD_IS_INSTALLED ~transitions.tp2~ ~0~) BEGIN
      OUTER_SPRINT ~BG1_BEFORE_TRANSITION~ ~GlobalLT("BD_PLOT","GLOBAL",41)~
      OUTER_SPRINT ~IT_IS_SOD~ ~GlobalGT("bd_plot","global",40)~
    END ELSE BEGIN
      OUTER_SPRINT ~BG1_BEFORE_TRANSITION~ ~Global("BD_PLOT","GLOBAL",0)~
      OUTER_SPRINT ~IT_IS_SOD~ ~GlobalGT("bd_plot","global",0)~
    END
  END
  ACTION_IF GAME_IS ~eet~ THEN BEGIN
    ACTION_IF (MOD_IS_INSTALLED ~c#endlessbg1.tp2~ ~14~) BEGIN
      OUTER_SPRINT ~BG1_BEFORE_TRANSITION~ ~OR(2) Global("ENDOFBG1","GLOBAL",0) GlobalLT("BD_PLOT","GLOBAL",41)~
      OUTER_SPRINT ~IT_IS_SOD~ ~Global("ENDOFBG1","GLOBAL",1) GlobalGT("bd_plot","global",40)~
    END ELSE ACTION_IF (MOD_IS_INSTALLED ~transitions.tp2~ ~0~) BEGIN
      OUTER_SPRINT ~BG1_BEFORE_TRANSITION~ ~OR(2) Global("ENDOFBG1","GLOBAL",0) GlobalLT("BD_PLOT","GLOBAL",41)~
      OUTER_SPRINT ~IT_IS_SOD~ ~Global("ENDOFBG1","GLOBAL",1) GlobalGT("bd_plot","global",40)~
    END ELSE BEGIN
      OUTER_SPRINT ~BG1_BEFORE_TRANSITION~ ~Global("ENDOFBG1","GLOBAL",0)~
      OUTER_SPRINT ~IT_IS_SOD~ ~Global("ENDOFBG1","GLOBAL",1)~
    END
  END

 ACTION_DEFINE_ARRAY fl#noconvert BEGIN INSTALL AJANTIS_MESSENGER.TRA AJANTIS_REPREACTIONS.TRA AJANTISBHAALLTS.TRA AJANTISBHAALLTS_RA1.TRA AJANTISDREAMLTS.TRA AJANTISINIFLIRTS_D.TRA AJANTISLTS.TRA AJANTISLTS_ADD.TRA AJANTISPC_PID.TRA AJANTISPCINIFLIRTS.TRA AJANTISROMNPC_TALKS.TRA ROM_INTERJECTIONS.TRA END

 ACTION_DEFINE_ARRAY fl#reload BEGIN game journal END

  LAF HANDLE_CHARSETS
    INT_VAR
      infer_charsets = 1
    STR_VAR
      tra_path = EVAL ~AjantisBG1/Translations/~
      noconvert_array = fl#noconvert
      reload_array = fl#reload
  END

/* vanilla BG1, general */
ACTION_IF GAME_IS ~bg1 totsc~ THEN BEGIN
PRINT @3

   INCLUDE ~AjantisBG1/lib/ajantis_bg1.tpa~

/* BG1 with TotSC installed */
   ACTION_IF GAME_IS ~totsc~ THEN BEGIN
	INCLUDE ~AjantisBG1/lib/bg1.tpa~
   END ELSE BEGIN 

/* BG1 without TotSC installed */
	INCLUDE ~AjantisBG1/lib/totsc.tpa~
   END
END 

/*
    //TRAs declared in LANGUAGE must be reloaded
  LOAD_TRA ~AjantisBG1/Translations/%LANGUAGE%/game.tra~
*/

END


AUTO_TRA ~AjantisBG1/Translations/%s~


LANGUAGE ~English~
         ~English~
         ~AjantisBG1/Translations/English/Setup-AjantisBG1.tra~
         ~AjantisBG1/Translations/English/game.tra~
         ~AjantisBG1/Translations/English/journal.tra~

LANGUAGE ~Francais~
         ~French~
         ~AjantisBG1/Translations/French/Setup-AjantisBG1.tra~
         ~AjantisBG1/Translations/French/game.tra~
         ~AjantisBG1/Translations/French/journal.tra~

LANGUAGE ~Deutsch~
         ~German~
         ~AjantisBG1/Translations/German/Setup-AjantisBG1.tra~
         ~AjantisBG1/Translations/German/game.tra~
         ~AjantisBG1/Translations/German/journal.tra~

LANGUAGE ~Simplified Chinese~
         ~schinese~
         ~AjantisBG1/Translations/Schinese/INSTALL.tra~
         ~AjantisBG1/Translations/Schinese/game.tra~
         ~AjantisBG1/Translations/Schinese/journal.tra~

BEGIN @0
REQUIRE_PREDICATE GAME_IS ~totsc tutu tutu_totsc bgt bgee eet~ @25
DESIGNATED 0
LABEL AjantisBG1Expansion-main
FORBID_COMPONENT ~EET_end.tp2~ ~0~ @26
FORBID_COMPONENT ~EET_end.tp2~ ~1~ @26





  /* STATE.IDS patching to ToB - thanks, Cam, if you read it */
  /* adds custom IsValidForPartyDialogue state */
  APPEND ~STATE.IDS~ ~0x80101FEF CD_STATE_NOTVALID~ UNLESS ~CD_STATE_NOTVALID~


/* Ajantis Friendship path */

   COMPILE EVALUATE_BUFFER ~AjantisBG1/Friendship/Ajantis_friendship_d.d~


ACTION_IF GAME_IS ~tutu tutu_totsc bgt bgee eet~ THEN BEGIN
/* for totsc scripts will be installed below */

EXTEND_TOP ~%tutu_var%ajantis.bcs~ ~AjantisBG1/Friendship/Ajantis_friendship.baf~
EVALUATE_BUFFER 

/* compatibility with BG1NPC romance */
ACTION_IF (MOD_IS_INSTALLED ~bg1npc.tp2~ ~20~) OR (MOD_IS_INSTALLED ~bg1npc.tp2~ ~21~) OR(MOD_IS_INSTALLED ~bg1npc.tp2~ ~22~) OR(MOD_IS_INSTALLED ~bg1npc.tp2~ ~23~) OR(MOD_IS_INSTALLED ~bg1npc.tp2~ ~24~) BEGIN
/* make sure friendshipt doesn't start because of too low rep for romance */
	EXTEND_TOP ~%tutu_var%ajantis.bcs~ ~AjantisBG1/Friendship/Ajantis_friendship_bg1npcrom.baf~
EVALUATE_BUFFER 
/* in case BG1NPC romance ends, Ajantis shouldn't start friendship track from the beginning */
	EXTEND_TOP ~%tutu_var%ajantis.bcs~ ~AjantisBG1/Friendship/Variable_transfer.baf~
EVALUATE_BUFFER
END 

END


/* more interjections */

ACTION_IF GAME_IS ~tutu tutu_totsc bgt bgee eet~ THEN BEGIN

/* give DV to cre */
ACTION_IF GAME_IS ~tutu tutu_totsc bgt~ THEN BEGIN
  /* Give FLAM5.cre a unique DV */
  COPY_EXISTING ~%tutu_var%FLAM5.CRE~ ~override~
    WRITE_EVALUATED_ASCII 0x280 ~FLAM5~ #32 // script name / DV
END

COMPILE EVALUATE_BUFFER ~AjantisBG1/Friendship/Tutu_Interjections.d~
  USING ~AjantisBG1/translations/%LANGUAGE%/BG_Interjections.tra~
EXTEND_BOTTOM ~%tutu_var%ajantis.bcs~ ~AjantisBG1/Friendship/Tutu_Interjections.baf~
EVALUATE_BUFFER 
  USING ~AjantisBG1/translations/%LANGUAGE%/BG_Interjections.tra~


END


/* content for EE: banter and romance conflicts */

ACTION_IF GAME_IS ~bgee eet~ THEN BEGIN
  COMPILE EVALUATE_BUFFER ~AjantisBG1/ee/Ajantis_ee.d~
  EXTEND_TOP ~%tutu_var%ajantis.bcs~ ~AjantisBG1/ee/Ajantis_ee.baf~
EVALUATE_BUFFER 
END


/* more content after BG1 ending and right before transition to BGII/SoD */

/* Sarevok is dead dialogue. Will only trigger for BG:EE/EET if EBG1 is installed, i.e. if game remains in BG1 after Sarevok's death. */
ACTION_IF GAME_IS ~bgt bgee eet~ THEN BEGIN

EXTEND_TOP ~%tutu_var%ajantis.bcs~ ~AjantisBG1/BG1_end/c#ajan_bg1end_bgt.baf~
EVALUATE_BUFFER 
COMPILE EVALUATE_BUFFER ~AjantisBG1/BG1_end/c#ajan_bg1end_bgt.d~ USING ~AjantisBG1/translations/%LANGUAGE%/ajantisbg1_transition.tra~
END

/* extra dialogue for transition for BG1 -> BGII: BGT */
ACTION_IF GAME_IS ~bgt~ THEN BEGIN
COMPILE EVALUATE_BUFFER ~AjantisBG1/BG1_end/ajantisbg1_bgt_transition.d~ USING ~AjantisBG1/translations/%LANGUAGE%/ajantisbg1_transition.tra~
END






//////////////////////////////////////////////////////////////////////////////
/* content for original BG1 game */

ACTION_IF GAME_IS ~bg1 totsc~ THEN BEGIN


INCLUDE ~AjantisBG1/BG1_TriggerSimulations/BG1AreaCheck_emulation.tpa~


/* spells for detection of morning talks: Player1 gets fake spell, that will be cast to activate a morning talk. After the next rest, the spell will be restored, and the morning dialogue can be triggered via "HaveSpell()" Thank you Zed Nocear! */

APPEND ~action.ids~ ~160 ApplySpellRES(S:ResRef*,O:Target*)~       UNLESS ~ApplySpellRES~

ACTION_IF (FILE_CONTAINS_EVALUATED (~dplayer3.BCS~ ~"Z!FATIG1"~))
    THEN BEGIN
        COPY_EXISTING ~dplayer3.BCS~ ~override~
            DECOMPILE_BCS_TO_BAF
            REPLACE_TEXTUALLY ~ApplySpellRES("Z!FATIG1",Player1)~
                                             ~ApplySpellRES("Z!FATIG1",Player1)
IncrementGlobal("X#AjantisRomanceRestCounter","GLOBAL",1) SetGlobalTimer("X#AjantisRomanceRestAfterTimer","GLOBAL",30)~
            COMPILE_BAF_TO_BCS
    END
    ELSE BEGIN
        EXTEND_TOP ~dplayer3.BCS~ ~AjantisBG1/BG1_TriggerSimulations/dplayer3_add.BAF~
        COPY ~AjantisBG1/BG1_TriggerSimulations/Z!FATIG1.SPL~ ~override~
    END

/* "Combat Counter" - Simulation, also from Zed Nocear */
//Give BioWare NPCs override scripts
COPY_EXISTING_REGEXP GLOB ~^alora[0-9]*\.cre~ ~override~
    WRITE_ASCII SCRIPT_OVERRIDE ~alora~
    READ_ASCII SCRIPT_CLASS ~Class_Script_Name~
    PATCH_IF ( ~%Class_Script_Name%~ STRING_EQUAL_CASE ~alora~ ) // Skript ~Alora~ wird aus Stelle CLASS geloescht, aber nicht Skript mit anderem Name

        THEN BEGIN
            WRITE_LONG 0x250 0x00000000
            WRITE_LONG 0x254 0x00000000
        END
    BUT_ONLY_IF_IT_CHANGES
COPY_EXISTING ~alora.BCS~ ~override~
    REPLACE_BCS_BLOCK ~AjantisBG1/BG1_TriggerSimulations/alora_old.baf~ ~AjantisBG1/BG1_TriggerSimulations/alora_new.baf~ //delete bad blocks
    BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING_REGEXP GLOB ~^branwe[0-9]*\.cre~ ~override~
	WRITE_ASCII SCRIPT_OVERRIDE ~branwen~
   BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING_REGEXP GLOB ~^faldor[0-9]*\.cre~ ~override~
	WRITE_ASCII SCRIPT_OVERRIDE ~faldorn~
   BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING_REGEXP GLOB ~^garric[0-9]*\.cre~ ~override~
	WRITE_ASCII SCRIPT_OVERRIDE ~garrick~
   BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING_REGEXP GLOB ~^imoen[0-9]+\.cre~ ~override~ // imoen1,2,4,6.CRE but not imoen.CRE
	WRITE_ASCII SCRIPT_OVERRIDE ~imoen~
   BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING_REGEXP GLOB ~^skie[0-9]*\.cre~ ~override~
	WRITE_ASCII SCRIPT_OVERRIDE ~skie~
   BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING_REGEXP GLOB ~^xan[0-9]*\.cre~ ~override~
   WRITE_LONG 0x248 0x00000000  
   WRITE_LONG 0x250 0x00000000  
   WRITE_ASCII SCRIPT_OVERRIDE ~xan~
   BUT_ONLY_IF_IT_CHANGES

//patching der Skripte für die CombatCounter() simulation
ACTION_IF (NOT FILE_CONTAINS_EVALUATED (~dplayer3.BCS~ ~Z!EnemyAlreadySeen~))
  THEN BEGIN
    EXTEND_BOTTOM ~dplayer3.BCS~ ~AjantisBG1/BG1_TriggerSimulations/combatcounter.baf~
  END
ACTION_IF (NOT FILE_CONTAINS_EVALUATED (~dplayer2.BCS~ ~Z!EnemyAlreadySeen~))
  THEN BEGIN
    EXTEND_BOTTOM ~dplayer2.BCS~ ~AjantisBG1/BG1_TriggerSimulations/combatcounter.baf~
  END
ACTION_IF (NOT FILE_CONTAINS_EVALUATED (~ajantis.BCS~ ~Z!EnemyAlreadySeen~))
  THEN BEGIN
    EXTEND_BOTTOM ~ajantis.BCS~  ~AjantisBG1/BG1_TriggerSimulations/combatcounter.baf~
  END
ACTION_IF (NOT FILE_CONTAINS_EVALUATED (~alora.BCS~ ~Z!EnemyAlreadySeen~))
  THEN BEGIN
    EXTEND_BOTTOM ~alora.BCS~    ~AjantisBG1/BG1_TriggerSimulations/combatcounter.baf~
  END
ACTION_IF (NOT FILE_CONTAINS_EVALUATED (~branwen.BCS~ ~Z!EnemyAlreadySeen~))
  THEN BEGIN
    EXTEND_BOTTOM ~branwen.BCS~  ~AjantisBG1/BG1_TriggerSimulations/combatcounter.baf~
  END
ACTION_IF (NOT FILE_CONTAINS_EVALUATED (~coran.BCS~ ~Z!EnemyAlreadySeen~))
  THEN BEGIN
    EXTEND_BOTTOM ~coran.BCS~    ~AjantisBG1/BG1_TriggerSimulations/combatcounter.baf~
  END
ACTION_IF (NOT FILE_CONTAINS_EVALUATED (~dynaheir.BCS~ ~Z!EnemyAlreadySeen~))
  THEN BEGIN
    EXTEND_BOTTOM ~dynaheir.BCS~ ~AjantisBG1/BG1_TriggerSimulations/combatcounter.baf~
  END
ACTION_IF (NOT FILE_CONTAINS_EVALUATED (~edwin.BCS~ ~Z!EnemyAlreadySeen~))
  THEN BEGIN
    EXTEND_BOTTOM ~edwin.BCS~    ~AjantisBG1/BG1_TriggerSimulations/combatcounter.baf~
  END
ACTION_IF (NOT FILE_CONTAINS_EVALUATED (~eldoth.BCS~ ~Z!EnemyAlreadySeen~))
  THEN BEGIN
    EXTEND_BOTTOM ~eldoth.BCS~   ~AjantisBG1/BG1_TriggerSimulations/combatcounter.baf~
  END
ACTION_IF (NOT FILE_CONTAINS_EVALUATED (~faldorn.BCS~ ~Z!EnemyAlreadySeen~))
  THEN BEGIN
    EXTEND_BOTTOM ~faldorn.BCS~  ~AjantisBG1/BG1_TriggerSimulations/combatcounter.baf~
  END
ACTION_IF (NOT FILE_CONTAINS_EVALUATED (~garrick.BCS~ ~Z!EnemyAlreadySeen~))
  THEN BEGIN
    EXTEND_BOTTOM ~garrick.BCS~  ~AjantisBG1/BG1_TriggerSimulations/combatcounter.baf~
  END
ACTION_IF (NOT FILE_CONTAINS_EVALUATED (~imoen.BCS~ ~Z!EnemyAlreadySeen~))
  THEN BEGIN
    EXTEND_BOTTOM ~imoen.BCS~    ~AjantisBG1/BG1_TriggerSimulations/combatcounter.baf~
  END
ACTION_IF (NOT FILE_CONTAINS_EVALUATED (~jaheira.BCS~ ~Z!EnemyAlreadySeen~))
  THEN BEGIN
    EXTEND_BOTTOM ~jaheira.BCS~  ~AjantisBG1/BG1_TriggerSimulations/combatcounter.baf~
  END
ACTION_IF (NOT FILE_CONTAINS_EVALUATED (~khalid.BCS~ ~Z!EnemyAlreadySeen~))
  THEN BEGIN
    EXTEND_BOTTOM ~khalid.BCS~   ~AjantisBG1/BG1_TriggerSimulations/combatcounter.baf~
  END
ACTION_IF (NOT FILE_CONTAINS_EVALUATED (~kagain.BCS~ ~Z!EnemyAlreadySeen~))
  THEN BEGIN
    EXTEND_BOTTOM ~kagain.BCS~   ~AjantisBG1/BG1_TriggerSimulations/combatcounter.baf~
  END
ACTION_IF (NOT FILE_CONTAINS_EVALUATED (~kivan.BCS~ ~Z!EnemyAlreadySeen~))
  THEN BEGIN
    EXTEND_BOTTOM ~kivan.BCS~    ~AjantisBG1/BG1_TriggerSimulations/combatcounter.baf~
  END
ACTION_IF (NOT FILE_CONTAINS_EVALUATED (~minsc.BCS~ ~Z!EnemyAlreadySeen~))
  THEN BEGIN
    EXTEND_BOTTOM ~minsc.BCS~    ~AjantisBG1/BG1_TriggerSimulations/combatcounter.baf~
  END
ACTION_IF (NOT FILE_CONTAINS_EVALUATED (~montaron.BCS~ ~Z!EnemyAlreadySeen~))
  THEN BEGIN
    EXTEND_BOTTOM ~montaron.BCS~ ~AjantisBG1/BG1_TriggerSimulations/combatcounter.baf~
  END
ACTION_IF (NOT FILE_CONTAINS_EVALUATED (~quayle.BCS~ ~Z!EnemyAlreadySeen~))
  THEN BEGIN
    EXTEND_BOTTOM ~quayle.BCS~   ~AjantisBG1/BG1_TriggerSimulations/combatcounter.baf~
  END
ACTION_IF (NOT FILE_CONTAINS_EVALUATED (~safana.BCS~ ~Z!EnemyAlreadySeen~))
  THEN BEGIN
    EXTEND_BOTTOM ~safana.BCS~   ~AjantisBG1/BG1_TriggerSimulations/combatcounter.baf~
  END
ACTION_IF (NOT FILE_CONTAINS_EVALUATED (~sharteel.BCS~ ~Z!EnemyAlreadySeen~))
  THEN BEGIN
    EXTEND_BOTTOM ~sharteel.BCS~ ~AjantisBG1/BG1_TriggerSimulations/combatcounter.baf~
  END
ACTION_IF (NOT FILE_CONTAINS_EVALUATED (~skie.BCS~ ~Z!EnemyAlreadySeen~))
  THEN BEGIN
    EXTEND_BOTTOM ~skie.BCS~     ~AjantisBG1/BG1_TriggerSimulations/combatcounter.baf~
  END
ACTION_IF (NOT FILE_CONTAINS_EVALUATED (~tiax.BCS~ ~Z!EnemyAlreadySeen~))
  THEN BEGIN
    EXTEND_BOTTOM ~tiax.BCS~     ~AjantisBG1/BG1_TriggerSimulations/combatcounter.baf~
  END
ACTION_IF (NOT FILE_CONTAINS_EVALUATED (~viconia.BCS~ ~Z!EnemyAlreadySeen~))
  THEN BEGIN
    EXTEND_BOTTOM ~viconia.BCS~  ~AjantisBG1/BG1_TriggerSimulations/combatcounter.baf~
  END
ACTION_IF (NOT FILE_CONTAINS_EVALUATED (~xan.BCS~ ~Z!EnemyAlreadySeen~))
  THEN BEGIN
    EXTEND_BOTTOM ~xan.BCS~      ~AjantisBG1/BG1_TriggerSimulations/combatcounter.baf~
  END
ACTION_IF (NOT FILE_CONTAINS_EVALUATED (~xzar.BCS~ ~Z!EnemyAlreadySeen~))
  THEN BEGIN
    EXTEND_BOTTOM ~xzar.BCS~     ~AjantisBG1/BG1_TriggerSimulations/combatcounter.baf~
  END
ACTION_IF (NOT FILE_CONTAINS_EVALUATED (~yeslick.BCS~ ~Z!EnemyAlreadySeen~))
  THEN BEGIN
    EXTEND_BOTTOM ~yeslick.BCS~  ~AjantisBG1/BG1_TriggerSimulations/combatcounter.baf~
END

  COPY ~AjantisBG1/Romance_BG1/Bam~ ~override~

/* Ajantis shield */
  COPY ~AjantisBG1/Romance_BG1/Items/X#AJSBG1.itm~ ~override~
    SAY NAME1 @4
    SAY NAME2 @5
    SAY UNIDENTIFIED_DESC @6
    SAY DESC @7
      BUT_ONLY_IF_IT_CHANGES


  /* Give Ajantis his shield */
  COPY_EXISTING ~ajanti.cre~  ~override~
                ~ajanti4.cre~ ~override~
                ~ajanti6.cre~ ~override~
    ADD_CRE_ITEM ~X#AJSBG1~ #0 #0 #0 ~IDENTIFIED~ ~SHIELD~
  BUT_ONLY_IF_IT_CHANGES


 /* items */
  COPY ~AjantisBG1/Romance_BG1/Items/X#AJNBG1.itm~ ~override~
    SAY NAME1 @8
    SAY NAME2 @8
    SAY UNIDENTIFIED_DESC @9
    SAY DESC @9

  COPY ~AjantisBG1/Romance_BG1/Items/X#A2NBG1.itm~ ~override~
    SAY NAME1 @8
    SAY NAME2 @8
    SAY UNIDENTIFIED_DESC @10
    SAY DESC @10


  COPY ~AjantisBG1/Romance_BG1/Items/X#AJRORI.itm~ ~override~
    SAY NAME1 @11
    SAY NAME2 @11
    SAY UNIDENTIFIED_DESC @12
    SAY DESC @12

  /* creatures */
  COPY ~AjantisBG1/Romance_BG1/CRE/X#AJDOUG.cre~ ~override~ //Douglas
    SAY NAME1 @13
    SAY NAME2 @13


EXTEND_TOP ~ajantis.bcs~ ~AjantisBG1/Romance_BG1/Scripts/Ajantisromnpc.baf~ 
EXTEND_TOP ~ajantis.bcs~ ~AjantisBG1/Romance_BG1/Scripts/ReputationReaction.baf~ EVALUATE_BUFFER 
EXTEND_TOP ~ajantis.bcs~ ~AjantisBG1/Friendship/Ajantis_friendship.baf~
EVALUATE_BUFFER 
EXTEND_TOP ~ajantis.bcs~ ~AjantisBG1/Romance_BG1/Scripts/AjantisRom.baf~ EVALUATE_BUFFER 
EXTEND_TOP ~ajantis.bcs~ ~AjantisBG1/Romance_BG1/Scripts/Ajantisiniflirts.baf~ 
EXTEND_TOP ~ajantis.bcs~ ~AjantisBG1/Friendship/Variable_transfer.baf~
EVALUATE_BUFFER 
EXTEND_TOP ~ajantis.bcs~ ~AjantisBG1/Romance_BG1/Scripts/AjantisRomCheckMatch.BAF~

COMPILE EVALUATE_BUFFER ~AjantisBG1/Romance_BG1/Scripts/x#ajdoug.baf~
COMPILE EVALUATE_BUFFER ~AjantisBG1/Romance_BG1/Scripts/x#ajrcut.baf~

COMPILE EVALUATE_BUFFER ~AjantisBG1/Romance_BG1/Dialogues/AjantisLTs.d~
COMPILE ~AjantisBG1/Romance_BG1/Dialogues/ajantisbhaallts.d~
COMPILE ~AjantisBG1/Romance_BG1/Dialogues/ajantisbhaallts_RA1.d~
COMPILE ~AjantisBG1/Romance_BG1/Dialogues/ajantisdreamlts.d~
COMPILE ~AjantisBG1/Romance_BG1/Dialogues/Ajantisiniflirts_d.d~
COMPILE EVALUATE_BUFFER ~AjantisBG1/Romance_BG1/Dialogues/Ajantis_RepReactions.d~ 
COMPILE ~AjantisBG1/Romance_BG1/Dialogues/AjantisRomnpc_talks.d~
COMPILE ~AjantisBG1/Romance_BG1/Dialogues/Ajantis_Messenger.d~

ACTION_IF FILE_EXISTS ~data/ExpAreas.bif~ THEN BEGIN
COMPILE ~AjantisBG1/Romance_BG1/Dialogues/AjantisLTs_Add.d~
COMPILE ~AjantisBG1/Romance_BG1/Dialogues/C#TROM.d~
COMPILE ~AjantisBG1/Romance_BG1/Dialogues/REPLACE_ROM.d~
COMPILE ~AjantisBG1/Romance_BG1/Dialogues/ROM_interjections.d~


END ELSE BEGIN 
COMPILE ~AjantisBG1/Romance_BG1/Dialogues/AjantisLTs_Add_noTotSC.d~
USING ~AjantisBG1/translations/%LANGUAGE%/AjantisLTs_Add.tra~
END

//Interjection. I_C_T does not work here.
COMPILE ~AjantisBG1/Friendship/C#DUMP.d~
COMPILE ~AjantisBG1/Friendship/REPLACE.d~
COMPILE ~AjantisBG1/Friendship/BG_Interjections.d~

//PIDs
COMPILE EVALUATE_BUFFER ~AjantisBG1/Romance_BG1/Dialogues/AjantisPC_PID.d~
COMPILE EVALUATE_BUFFER ~AjantisBG1/Romance_BG1/Dialogues/AjantisPCiniflirts.d~



/* tavern-script blocks for "night talk" */

EXTEND_BOTTOM ~AR1001.BCS~ ~AjantisBG1/Romance_BG1/Scripts/C#Tavern_AD.BAF~
EXTEND_BOTTOM ~AR0116.BCS~ ~AjantisBG1/Romance_BG1/Scripts/C#Tavern_AD.BAF~
EXTEND_BOTTOM ~AR0119.BCS~ ~AjantisBG1/Romance_BG1/Scripts/C#Tavern_AD.BAF~
EXTEND_BOTTOM ~AR0103.BCS~ ~AjantisBG1/Romance_BG1/Scripts/C#Tavern_AD.BAF~
EXTEND_BOTTOM ~AR0114.BCS~ ~AjantisBG1/Romance_BG1/Scripts/C#Tavern_AD.BAF~
EXTEND_BOTTOM ~AR0705.BCS~ ~AjantisBG1/Romance_BG1/Scripts/C#Tavern_AD.BAF~
EXTEND_BOTTOM ~AR0105.BCS~ ~AjantisBG1/Romance_BG1/Scripts/C#Tavern_AD.BAF~
EXTEND_BOTTOM ~AR0133.BCS~ ~AjantisBG1/Romance_BG1/Scripts/C#Tavern_AD.BAF~
EXTEND_BOTTOM ~AR2301.BCS~ ~AjantisBG1/Romance_BG1/Scripts/C#Tavern_AD.BAF~
EXTEND_BOTTOM ~AR3304.BCS~ ~AjantisBG1/Romance_BG1/Scripts/C#Tavern_AD.BAF~
EXTEND_BOTTOM ~AR3307.BCS~ ~AjantisBG1/Romance_BG1/Scripts/C#Tavern_AD.BAF~
EXTEND_BOTTOM ~AR3351.BCS~ ~AjantisBG1/Romance_BG1/Scripts/C#Tavern_AD.BAF~
EXTEND_BOTTOM ~AR3357.BCS~ ~AjantisBG1/Romance_BG1/Scripts/C#Tavern_AD.BAF~

END //BG1


/* crossmod with NTotSC - for all games */

/* no check needed - if NTotSC is not installed, it will not show */

COMPILE EVALUATE_BUFFER ~AjantisBG1/crossmod/ntotsc.d~ USING ~AjantisBG1/translations/%LANGUAGE%/crossmod.tra~
EXTEND_TOP ~%tutu_var%ajantis.bcs~ ~AjantisBG1/crossmod/ntotsc.baf~
EVALUATE_BUFFER 

/* crossmod for Transitions: good-bye dialogues */

ACTION_IF (MOD_IS_INSTALLED ~transitions/setup-transitions.tp2~ ~0~) THEN BEGIN
  PRINT @23
  COMPILE EVALUATE_BUFFER ~ajantisbg1/crossmod/ajantis_transitions.d~
	USING ~AjantisBG1/translations/%LANGUAGE%/AJANTISBG1_TRANSITION.TRA~
  COMPILE EVALUATE_BUFFER ~ajantisbg1/crossmod/ajantis_transitions_endlessbg1_korlasz_tomb.d~
	USING ~AjantisBG1/translations/%LANGUAGE%/AJANTISBG1_TRANSITION.TRA~
  EXTEND_TOP ~ajantis.bcs~ ~ajantisbg1/crossmod/ajantis_transitions.baf~
    EVALUATE_BUFFER 
  EXTEND_TOP ~ajantis.bcs~ ~ajantisbg1/BG1_end/c#ajan_bg1end_sod.baf~
    EVALUATE_BUFFER 
/* Ajantis should not remain dead in case he was upon transition */
  EXTEND_TOP ~#L_CUT09.bcs~ ~AjantisBG1/crossmod/ajantis_transitions_eet.baf~ 
    EVALUATE_BUFFER
END


/* EndlessBG1 */
ACTION_IF (GAME_INCLUDES ~sod~ AND MOD_IS_INSTALLED ~c#endlessbg1.tp2~ ~0~) THEN BEGIN 

PRINT @27 /* ~EndlessBG1 detected: Installing crossmod...~ */

/* direct transition to BGII */
// Get state @8 for BELT %BELT_ebg1_8% 
/* ~I see. Still, the city is in your debt. Fare well, Hero of Baldur's Gate.~ */
  OUTER_SET BELT_ebg1_8 = STATE_WHICH_SAYS 8 IN ~c#endlessbg1/translations/%s/DIALOGUES.TRA~ FROM ~BELT~
<<<<<<<< ...inlined/ajantis_ebg1_bg1_transition.d
I_C_T3 ~BELT~ %BELT_ebg1_8% C#AjantisBG1_Leaving
/* non-romance case */
== ~%AJANTIS_JOINED%~ IF ~InParty("Ajantis")
	InMyArea("Ajantis") 
!StateCheck("Ajantis",CD_STATE_NOTVALID)
!Global("X#AjantisRomanceActive","GLOBAL",2)~ THEN @0 /* ~It was an honor to cleanse this place from the bandit threat and the rising war with you. But now Sarevok and his evil followers are defeated. I shall return home to call on the Order of the Radiant Heart. I will report the happenings and your part in it. I, ahem, will also mention my part in it, and hope I will be found worthy to receive knighthood. I thank you for combining forces with me.~ */ DO ~SetGlobal("IWasKickedOut","LOCALS",0)
TakePartyItem("x#ajshld")
DestroyItem("x#ajshld")
GivePartyAllEquipment()
LeaveParty()
ChangeAIScript("",DEFAULT)
SetLeavePartyDialogFile()
EscapeArea()~ 
/* romance case */
== ~%AJANTIS_JOINED%~ IF ~InParty("Ajantis")
	InMyArea("Ajantis") 
!StateCheck("Ajantis",CD_STATE_NOTVALID) 
Global("X#AjantisRomanceActive","GLOBAL",2)~ THEN @1 /* ~<CHARNAME>, I fear time has come to say good bye. The war is stopped, Sarevok is defeated. It is time for me to report to the Order of the Radiant Heart. But I will travel to Waterdeep first, as we already agreed upon. Nothing will detain me from seeking my parents' answer concerning our plans for marriage, now that our foe is defeated and peace has come back to the lands, at last. I am so looking foreward to talking to them, <CHARNAME>, my love. Talking to them and telling them of the love I hold for you.~ */
== ~%AJANTIS_JOINED%~ IF ~InParty("Ajantis")
	InMyArea("Ajantis") 
!StateCheck("Ajantis",CD_STATE_NOTVALID)
Global("X#AjantisRomanceActive","GLOBAL",2)~ THEN @2 /* ~My heart aches to leave you. I will send you messages about my whereabouts as often as I can. And be it Waterdeep, Athkatla, or here that we meet again, be assured I will come for you - or await you, no matter where we are destined to be reunited. Farewell, 'Hero of Baldur's Gate' (smiles). Relish the people's gratitude and praise while they are still willing to give it, <CHARNAME>. You earned it.~ */
DO ~SetGlobal("IWasKickedOut","LOCALS",0)
TakePartyItem("x#ajshld")
DestroyItem("x#ajshld")
GivePartyAllEquipment()
LeaveParty()
ChangeAIScript("",DEFAULT)
SetLeavePartyDialogFile()
EscapeArea()~
END
>>>>>>>>
COMPILE EVALUATE_BUFFER ~...inlined/ajantis_ebg1_bg1_transition.d~ 
USING ~AjantisBG1/translations/%LANGUAGE%/ajantisbg1_transition.tra~

/* Crossmod with EndlessBG1 "Korlasz Tomb in BG1 */
ACTION_IF (MOD_IS_INSTALLED ~c#endlessbg1.tp2~ ~14~) BEGIN

/* make sure Ajantis' SoD override script is used in Korlasz' Crypt */
COPY_EXISTING ~bd0120.bcs~ ~override~
DECOMPILE_AND_PATCH BEGIN
		SPRINT textToReplace ~\(ActionOverride("Ajantis",ChangeAIScript("",OVERRIDE))\)~
		COUNT_REGEXP_INSTANCES ~%textToReplace%~ num_matches
		PATCH_IF (num_matches > 0) BEGIN
			REPLACE_TEXTUALLY ~%textToReplace%~ ~ActionOverride("Ajantis",ChangeAIScript("BDAJANTI",OVERRIDE))~
			PATCH_PRINT ~Patching: %num_matches% matches found in %SOURCE_FILESPEC% for REPLACE_TEXTUALLY: %textToReplace%~
		END ELSE BEGIN
			PATCH_WARN ~WARNING: could not find %textToReplace% in %SOURCE_FILESPEC%~
		END
END
BUT_ONLY_IF_IT_CHANGES
COPY_EXISTING ~bd0130.bcs~ ~override~
DECOMPILE_AND_PATCH BEGIN
		SPRINT textToReplace ~\(ActionOverride("Ajantis",ChangeAIScript("",OVERRIDE))\)~
		COUNT_REGEXP_INSTANCES ~%textToReplace%~ num_matches
		PATCH_IF (num_matches > 0) BEGIN
			REPLACE_TEXTUALLY ~%textToReplace%~ ~ActionOverride("Ajantis",ChangeAIScript("BDAJANTI",OVERRIDE))~
			PATCH_PRINT ~Patching: %num_matches% matches found in %SOURCE_FILESPEC% for REPLACE_TEXTUALLY: %textToReplace%~
		END ELSE BEGIN
			PATCH_WARN ~WARNING: could not find %textToReplace% in %SOURCE_FILESPEC%~
		END
END
BUT_ONLY_IF_IT_CHANGES
/* not necessary here: will be done later anyhow!
COPY_EXISTING ~bdintro.bcs~ ~override~
DECOMPILE_AND_PATCH BEGIN
		SPRINT textToReplace ~\(ActionOverride("Ajantis",ChangeAIScript("",OVERRIDE))\)~
		COUNT_REGEXP_INSTANCES ~%textToReplace%~ num_matches
		PATCH_IF (num_matches > 0) BEGIN
			REPLACE_TEXTUALLY ~%textToReplace%~ ~ActionOverride("Ajantis",ChangeAIScript("BDAJANTI",OVERRIDE))~
			PATCH_PRINT ~Patching: %num_matches% matches found in %SOURCE_FILESPEC% for REPLACE_TEXTUALLY: %textToReplace%~
		END ELSE BEGIN
			PATCH_WARN ~WARNING: could not find %textToReplace% in %SOURCE_FILESPEC%~
		END
END
BUT_ONLY_IF_IT_CHANGES
*/

END //~c#endlessbg1.tp2~ ~14~

END /* EndlessBG1 */

/* transition from BG1 to SoD: in case Ajantis was in party but dead, he will be silently revived */
ACTION_IF (GAME_INCLUDES ~sod~) THEN BEGIN
  /* only install this if it isn't installed already */
  ACTION_IF NOT (MOD_IS_INSTALLED ~ajantisbg2/setup-ajantisbg2.TP2~ ~0~) THEN BEGIN
    EXTEND_TOP ~BD0103.bcs~ ~AjantisBG1/ee/sod_transition.baf~ 
  EVALUATE_BUFFER
  /* this is for the case that the player skips SoD in EET */
    ACTION_IF GAME_IS ~eet~ THEN BEGIN
      EXTEND_TOP ~K#TELBGT.bcs~ ~AjantisBG1/ee/eet_transition.baf~ 
  EVALUATE_BUFFER
    END
  END
END

///////////////////////////////////////////////////////////
// ~Ajantis NPC for SoD~ 

ACTION_IF (GAME_IS ~bgee eet~ AND FILE_EXISTS_IN_GAME ~bd0103.are~) THEN BEGIN
PRINT @90000 /* Detecting SoD: Installing Ajantis NPC for SoD */

ADD_JOURNAL TITLE (@100004) @100000 @100001 @100002 @100003

/* Ajantis's SoD cre file */

COPY_EXISTING ~ajanti6.cre~ ~override/c#ajsod6.cre~ 
//SAY NAME1 #6131 
//SAY NAME2 #6131
//WRITE_EVALUATED_ASCII 0x280 ~Ajantis~ #32 // script name / DV
WRITE_EVALUATED_ASCII 0x2cc ~C#AjSoDG~ #8 //greetings dialogue
WRITE_EVALUATED_ASCII 0x248 ~BDAJANTI~ #8   // Override script
WRITE_EVALUATED_ASCII 0x250 ~BDAJANTC~ #8   // Class script
//WRITE_EVALUATED_ASCII 0x258 ~~ #8   // Race script
//WRITE_EVALUATED_ASCII 0x260 ~~ #8   // General script
//WRITE_EVALUATED_ASCII 0x268 ~~ #8   // Default script
    ADD_CRE_ITEM ~SW1H02~ #0 #0 #0 ~IDENTIFIED~ ~WEAPON1~ EQUIP
    ADD_CRE_ITEM ~PLAT10~ #0 #0 #0 ~IDENTIFIED~ ~ARMOR~ 


/* Content for Korlasz' Crypt */
/* comment after Korlasz is defeated */

COPY_EXISTING ~bdintro.bcs~ ~override~
DECOMPILE_AND_PATCH BEGIN
		SPRINT textToReplace ~\(ActionOverride("Ajantis",ChangeAIScript("",OVERRIDE))\)~
		COUNT_REGEXP_INSTANCES ~%textToReplace%~ num_matches
		PATCH_IF (num_matches > 0) BEGIN
			REPLACE_TEXTUALLY ~%textToReplace%~ ~ActionOverride("Ajantis",ChangeAIScript("BDAJANTI",OVERRIDE))~
			PATCH_PRINT ~Patching: %num_matches% matches found in %SOURCE_FILESPEC% for REPLACE_TEXTUALLY: %textToReplace%~
		END ELSE BEGIN
			PATCH_WARN ~WARNING: could not find %textToReplace% in %SOURCE_FILESPEC%~
		END
END
BUT_ONLY_IF_IT_CHANGES

/* make sure Ajantis only complains about Korlasz' death if she actually tried to surrender */
EXTEND_TOP ~BD0130.bcs~ ~AjantisBG1/ee/ajbg1_bd0130.baf~ EVALUATE_BUFFER

/* mute Ajantis' Farewell-DisplayStringHeads in case of romance */
EXTEND_TOP ~BD0120.bcs~ ~AjantisBG1/ee/ajbg1_bd0120_rom.baf~ EVALUATE_BUFFER
EXTEND_TOP ~BD0130.bcs~ ~AjantisBG1/ee/ajbg1_bd0120_rom.baf~ EVALUATE_BUFFER

/* correct his farewell DisplayStringHead: "Athkatla" instead of "Waterdeep". There is no Order of the Radiant Heart in Waterdeep.
#57802  // Though it makes my heart heavy, I must soon return to Waterdeep. */
STRING_SET_EVALUATE (GAME_IS ~eet~ ? 257802 : 57802) @24 /* ~Though it makes my heart heavy, I must soon return to Athkatla.~ */

EXTEND_TOP ~BDAJANTI.bcs~ ~AjantisBG1/BG1_end/c#ajan_bg1end_sod.baf~
EVALUATE_BUFFER 
COMPILE EVALUATE_BUFFER ~AjantisBG1/BG1_end/c#ajan_bg1end_sod.d~ USING ~AjantisBG1/translations/%LANGUAGE%/ajantisbg1_transition.tra~
COMPILE EVALUATE_BUFFER ~AjantisBG1/BG1_end/ajantisbg1_sod_transition.d~ USING ~AjantisBG1/translations/%LANGUAGE%/ajantisbg1_transition.tra~


/* Ajantis's dialogues */

/* (T1) SoD greeting dialogue */
COMPILE EVALUATE_BUFFER ~ajantisbg1/ajantissod/dialogue/greetingsdialogue.d~

/* (T1) SoD kickout dialogue */
COMPILE EVALUATE_BUFFER ~ajantisbg1/ajantissod/dialogue/kickoutdialogue.d~ USING ~AjantisBG1/translations/%LANGUAGE%/greetingsdialogue.tra~

/* (T2/T3)) Ajantis's comments on SoD events */
COMPILE EVALUATE_BUFFER ~ajantisbg1/ajantissod/dialogue/sod_event_comments.d~

/* Ajantis's reactions to important knowledge about Caelar's plans. Works best with Road to Discovery installed. */
COMPILE EVALUATE_BUFFER ~ajantisbg1/ajantissod/dialogue/sod_important_reactions_dialogues.d~

/* Ajantis' SoD Lovetalk(s) */
COMPILE EVALUATE_BUFFER ~ajantisbg1/ajantissod/dialogue/dialogues_sod.d~

/* quest dialogues */
COMPILE EVALUATE_BUFFER ~ajantisbg1/ajantissod/dialogue/sod_quest.d~


/* script compilation */

/* Ajantis's reactions to important knowledge about Caelar's plans. Works best with Road to Discovery installed. */
EXTEND_BOTTOM ~BDAJANTI.bcs~ ~ajantisbg1/ajantissod/baf/sod_important_reactions_scripts.baf~
  EVALUATE_BUFFER	
EXTEND_BOTTOM ~BDAJANTI.bcs~ ~ajantisbg1/ajantissod/baf/bdajanti_patch.baf~ EVALUATE_BUFFER
EXTEND_BOTTOM ~BDAJANTI.bcs~ ~ajantisbg1/ajantissod/baf/general_script_additions.baf~
  EVALUATE_BUFFER
/*
EXTEND_BOTTOM ~BDAJANTI.bcs~ ~ajantisbg1/ajantissod/baf/bdajanti_patch_quest.baf~
  EVALUATE_BUFFER
*/

/* empty Timer setting */

<<<<<<<< ...inlined/C#AjSoD_timer.baf
/* make sure this is at the END of all script blocks using this timer or any script block using this timer elsewhere will never trigger! */
IF
	InParty("Ajantis")
	CombatCounter(0)
	!See([ENEMY])
	ActionListEmpty()
	!RealGlobalTimerNotExpired("C#AjSoD_DialogReactionTimer","GLOBAL")
THEN
	RESPONSE #100
		RealSetGlobalTimer("C#AjSoD_DialogReactionTimer","GLOBAL",900)
END
>>>>>>>>
EXTEND_BOTTOM ~BDAJANTI.bcs~ ~...inlined/C#AjSoD_timer.baf~


/* script compilation: modify existing game scripts. In alphabetical order with regard to ingame scripts. */

/* (T1) let Ajantis (re)appear in front of the Duchal Palace */
EXTEND_BOTTOM ~bd0010.bcs~ ~ajantisbg1/ajantissod/baf/bd0010_patch.baf~ EVALUATE_BUFFER

/* (T3) Comment in random area (Canyon Ambush) bd0063.are */
EXTEND_BOTTOM ~bd0063.bcs~ ~ajantisbg1/ajantissod/baf/bd0063_commenting.baf~
EVALUATE_BUFFER

/* (T3) Waiting to march out of BG city: make Ajantis comment while waiting to leave */
EXTEND_TOP ~bd0101.bcs~ ~ajantisbg1/ajantissod/baf/bd0101_commenting_et.baf~
  EVALUATE_BUFFER

/* (T1) Ajantis is in front of Ducal Palace if told to wait - or Player marches onto the battle without recruiting NPCs in Baldur' Gate: Ajantis will be in front of the palace, too */
EXTEND_BOTTOM ~bd0101.bcs~ ~ajantisbg1/ajantissod/baf/bd0101_patch.baf~
  EVALUATE_BUFFER

/* (T3) In case Ajantis is in party the last night the PC slept in Ducal Palace - comment after Skie is gone */
EXTEND_TOP ~bd0103.bcs~   ~ajantisbg1/ajantissod/baf/bd0103_commenting_et.baf~  
  EVALUATE_BUFFER

/* (T1) after Korlasz is defeated: PC is moved to Ducal Palace. Joined NPCs leave -> handled by game or by BG1 part */

/* (T2) Ajantis visits PC in Prison */
EXTEND_TOP ~bd0104.bcs~ ~ajantisbg1/ajantissod/baf/bd0104_patch.baf~
  EVALUATE_BUFFER

/* (T3) Comment after killing the blind Wyrmlings in Cave (bd0113.bcs) */
EXTEND_BOTTOM ~bd0113.bcs~ ~ajantisbg1/ajantissod/baf/bd0113_commenting.baf~
EVALUATE_BUFFER

/* (T3) Comment after killing the Rhinoceros Beetle in bd0114.are */
EXTEND_BOTTOM ~bd0114.bcs~ ~ajantisbg1/ajantissod/baf/bd0114_commenting.baf~
EVALUATE_BUFFER

/* (T3) comment after defeating the first enemies in Korlasz' Crypt */
EXTEND_TOP ~bd0120.bcs~ ~ajantisbg1/ajantissod/baf/bd0120_commenting_et.baf~
  EVALUATE_BUFFER

/* (T1) Ajantis moves to war camp in bd1000.are */
EXTEND_TOP ~bd1000.bcs~ ~ajantisbg1/ajantissod/baf/bd1000_patch.baf~
  EVALUATE_BUFFER

/* (T3) Reaction to Caelar's first appearance on bridge */
EXTEND_BOTTOM ~bd1000.bcs~ ~ajantisbg1/ajantissod/baf/bd1000_commenting.baf~
  EVALUATE_BUFFER

/* (T1) Patch your NPC's "Move Camp" variable to the original game's script block (bd1000.bcs) */
COPY_EXISTING ~bd1000.BCS~ ~override~
	DECOMPILE_AND_PATCH BEGIN
		SPRINT textToReplace ~\(SetGlobal("bd_move_npcs","bd1000",1)\)~
		COUNT_REGEXP_INSTANCES ~%textToReplace%~ num_matches
		PATCH_IF (num_matches > 0) BEGIN
			REPLACE_TEXTUALLY ~%textToReplace%~ ~\1 SetGlobal("C#AjSoD_MoveCamp","MYAREA",1)~ 
			PATCH_PRINT ~Patching: %num_matches% matches found in %SOURCE_FILESPEC% for REPLACE_TEXTUALLY: %textToReplace%~
		END ELSE BEGIN
			PATCH_WARN ~WARNING: could not find %textToReplace% in %SOURCE_FILESPEC%~
		END
	END
BUT_ONLY

/* (T3) Battle at Bridgefort starts */
EXTEND_BOTTOM ~bd2000.bcs~ ~ajantisbg1/ajantissod/baf/bd2000_commenting.baf~
  EVALUATE_BUFFER

/* (T3) PC gave Bridgefort Castle to the crusaders */
EXTEND_TOP ~bd2000.bcs~ ~ajantisbg1/ajantissod/baf/bd2000_commenting_et.baf~
  EVALUATE_BUFFER

/* (T1) Last camp is in bd3000.are. In case Ajantis was never in party or kicked out to wait somewhere else, he will also be back in the (new) camp. */
EXTEND_TOP ~bd3000.bcs~ ~ajantisbg1/ajantissod/baf/bd3000_patch.baf~
  EVALUATE_BUFFER

/* (T1) Patch your NPC's "Move Camp" variable to the original game's script block (bd3000.bcs) */
COPY_EXISTING ~bd3000.BCS~ ~override~
	DECOMPILE_AND_PATCH BEGIN
		SPRINT textToReplace ~\(SetGlobal("bd_move_npcs","bd3000",1)\)~
		COUNT_REGEXP_INSTANCES ~%textToReplace%~ num_matches
		PATCH_IF (num_matches > 0) BEGIN
			REPLACE_TEXTUALLY ~%textToReplace%~ ~\1 SetGlobal("C#AjSoD_MoveCamp","MYAREA",1)~ 
			PATCH_PRINT ~Patching: %num_matches% matches found in %SOURCE_FILESPEC% for REPLACE_TEXTUALLY: %textToReplace%~
		END ELSE BEGIN
			PATCH_WARN ~WARNING: could not find %textToReplace% in %SOURCE_FILESPEC%~
		END
	END
BUT_ONLY

/* (T1) Patch your NPC's "Move Camp" variable to the original game's script block (bd3000.bcs)  to call back NPC when battle starts */
COPY_EXISTING ~bd3000.BCS~ ~override~
	DECOMPILE_AND_PATCH BEGIN
		SPRINT textToReplace ~\(SetGlobal("bd_move_voghiln","bd3000",0)\)~
		COUNT_REGEXP_INSTANCES ~%textToReplace%~ num_matches
		PATCH_IF (num_matches > 0) BEGIN
			REPLACE_TEXTUALLY ~%textToReplace%~ ~\1 SetGlobal("C#AjSoD_MoveCamp","MYAREA",0)~ 
			PATCH_PRINT ~Patching: %num_matches% matches found in %SOURCE_FILESPEC% for REPLACE_TEXTUALLY: %textToReplace%~
		END ELSE BEGIN
			PATCH_WARN ~WARNING: could not find %textToReplace% in %SOURCE_FILESPEC%~
		END
	END
BUT_ONLY

/* (T2) Ajantis comments on murder accusation */
EXTEND_TOP ~bd4100.bcs~ ~ajantisbg1/ajantissod/baf/bd4100_patch.baf~
  EVALUATE_BUFFER

/* (T3) Comments in bd4300.are (Bridgefort Castle interior and Avernus portal) */
EXTEND_TOP ~bd4300.bcs~ ~ajantisbg1/ajantissod/baf/bd4300_commenting_et.baf~
EVALUATE_BUFFER	

/* (T3) Comment upon entering Avernus */
EXTEND_BOTTOM ~bd4400.bcs~ ~ajantisbg1/ajantissod/baf/bd4400_commenting.baf~
EVALUATE_BUFFER

/* (T3) Comment in the Avernus Elevator */
EXTEND_BOTTOM ~bd4601.bcs~ ~ajantisbg1/ajantissod/baf/bd4601_commenting.baf~
EVALUATE_BUFFER

/* (T3) Comment before entering the Cave with the blind Wyrmlings (bd5100aw.bcs) */
EXTEND_BOTTOM ~bd5100aw.bcs~ ~ajantisbg1/ajantissod/baf/bd5100aw_commenting.baf~
EVALUATE_BUFFER

/* (T3) Comment when battle starts in Kanaglym (bd5300.are, Underground River) */
EXTEND_BOTTOM ~bd5300.bcs~ ~ajantisbg1/ajantissod/baf/bd5300_commenting.baf~
EVALUATE_BUFFER

/* (T1) and the same for the next camp in bd7100.are: */
EXTEND_BOTTOM ~bd7100.bcs~ ~ajantisbg1/ajantissod/baf/bd7100_patch.baf~
  EVALUATE_BUFFER

/* (T1) Patch your NPC's "Move Camp" variable to the original game's script block (bd7100.bcs) */
COPY_EXISTING ~bd7100.BCS~ ~override~
	DECOMPILE_AND_PATCH BEGIN
		SPRINT textToReplace ~\(SetGlobal("bd_move_npcs","bd7100",1)\)~
		COUNT_REGEXP_INSTANCES ~%textToReplace%~ num_matches
		PATCH_IF (num_matches > 0) BEGIN
			REPLACE_TEXTUALLY ~%textToReplace%~ ~\1 SetGlobal("C#AjSoD_MoveCamp","MYAREA",1)~ 
			PATCH_PRINT ~Patching: %num_matches% matches found in %SOURCE_FILESPEC% for REPLACE_TEXTUALLY: %textToReplace%~
		END ELSE BEGIN
			PATCH_WARN ~WARNING: could not find %textToReplace% in %SOURCE_FILESPEC%~
		END
	END
BUT_ONLY

/* (T1) Move NPC to entrance of the Allied Siege Camp (Crusade battle) */
EXTEND_BOTTOM ~bdasc3.bcs~ ~ajantisbg1/ajantissod/baf/bdasc3_patch.baf~
  EVALUATE_BUFFER

/* (T3) Comment in Dragonspear Castle Exterior */
EXTEND_BOTTOM ~bdbark01.bcs~ ~ajantisbg1/ajantissod/baf/bdbark01_commenting.baf~
EVALUATE_BUFFER

/* (T3) Move the PC in the cutscene of the plotting nobles in Three Old Kegs (bdc116d.bcs) */
EXTEND_TOP ~bdc116d.bcs~ ~ajantisbg1/ajantissod/baf/bdc116d_patch.baf~
EVALUATE_BUFFER

/*  (T3) March out of BG city: make Ajantis move when the coalition start marching out of BG city */
EXTEND_TOP ~bdcut08.bcs~ ~ajantisbg1/ajantissod/baf/bdcut08_patch.baf~
  EVALUATE_BUFFER

/* (T1) Ajantis is kicked in hell dimension - teleport to last hell area bd4400.are */
EXTEND_TOP ~bdcut58.bcs~ ~ajantisbg1/ajantissod/baf/bdcut58_patch.baf~
  EVALUATE_BUFFER

/* (T1) Ajantis is kicked in hell dimension or was waiting elsewhere- teleport to Dragonspear Castle interior (bd4300.are) after PC returns from Avernus */
EXTEND_TOP ~bdcut59b.bcs~ ~ajantisbg1/ajantissod/baf/bdcut59b_patch.baf~
  EVALUATE_BUFFER

/* (T2) after PC gets arrested for murder: Ajantis leaves party */
EXTEND_TOP ~bdcut61.bcs~ ~ajantisbg1/ajantissod/baf/bdcut61_patch.baf~
  EVALUATE_BUFFER

/* (T3) React in case crusaders in cages are killed by spikes (bd7230.are) */
/* Patch bdlever2.bcs */
COPY_EXISTING ~bdlever2.BCS~ ~override~
	DECOMPILE_AND_PATCH BEGIN
		SPRINT textToReplace ~\(Kill("bdkharmy")\)~
		COUNT_REGEXP_INSTANCES ~%textToReplace%~ num_matches
		PATCH_IF (num_matches > 0) BEGIN
			REPLACE_TEXTUALLY ~%textToReplace%~ ~SetGlobal("C#AjSoD_UsedSpikes","GLOBAL",1) \1~ 
			PATCH_PRINT ~Patching: %num_matches% matches found in %SOURCE_FILESPEC% for REPLACE_TEXTUALLY: %textToReplace%~
		END ELSE BEGIN
			PATCH_WARN ~WARNING: could not find %textToReplace% in %SOURCE_FILESPEC%~
		END
		SPRINT textToReplace ~\(ActionOverride(Player1,RemovePaladinHood())\)~
		COUNT_REGEXP_INSTANCES ~%textToReplace%~ num_matches
		PATCH_IF (num_matches > 0) BEGIN
			REPLACE_TEXTUALLY ~%textToReplace%~ ~SetGlobal("C#AjSoD_SpikeTrapFall","GLOBAL",1) \1~ 
			PATCH_PRINT ~Patching: %num_matches% matches found in %SOURCE_FILESPEC% for REPLACE_TEXTUALLY: %textToReplace%~
		END ELSE BEGIN
			PATCH_WARN ~WARNING: could not find %textToReplace% in %SOURCE_FILESPEC%~
		END
		SPRINT textToReplace ~\(ActionOverride(Player1,RemoveRangerHood())\)~
		COUNT_REGEXP_INSTANCES ~%textToReplace%~ num_matches
		PATCH_IF (num_matches > 0) BEGIN
			REPLACE_TEXTUALLY ~%textToReplace%~ ~SetGlobal("C#AjSoD_SpikeTrapFall","GLOBAL",1) \1~ 
			PATCH_PRINT ~Patching: %num_matches% matches found in %SOURCE_FILESPEC% for REPLACE_TEXTUALLY: %textToReplace%~
		END ELSE BEGIN
			PATCH_WARN ~WARNING: could not find %textToReplace% in %SOURCE_FILESPEC%~
		END
	END
BUT_ONLY

/* (T1) return to camp if kicked out */
EXTEND_TOP ~bdparty.bcs~ ~ajantisbg1/ajantissod/baf/bdparty_patch.baf~
  EVALUATE_BUFFER

/* last romance dialogue before going down the portal */
EXTEND_TOP ~bdport01.bcs~ ~ajantisbg1/ajantissod/baf/bdport01_patch_et.baf~
  EVALUATE_BUFFER

/* (T3) Comment on closed vault door (to portal) in Dragonspear Castle Interior */
EXTEND_BOTTOM ~bdvaultd.bcs~ ~ajantisbg1/ajantissod/baf/bdvaultd_patch.baf~
EVALUATE_BUFFER


/* romance reactions: add reply options so PC can shut down SoD romances */
COMPILE EVALUATE_BUFFER ~ajantisbg1/ajantissod/dialogue/sod_romancereactions.d~ 

/* SoD "content" for romance case: Ajantis will write letters */

/* first letter */
  COPY ~ajantisbg1/ajantissod/item/C#AJBG11.itm~ ~override/C#AJBG11.itm~
    SAY NAME1 @19
    SAY NAME2 @19
    SAY DESC @20

/* first letter */
EXTEND_BOTTOM ~BD0103.bcs~ ~ajantisbg1/ajantissod/baf/ajbg1_bd0103.baf~ EVALUATE_BUFFER

/* sod quest: everything except Ajantis' script and dialogue */

EXTEND_TOP ~bd3000.bcs~ ~ajantisbg1/ajantissod/baf/bd3000_quest.baf~
  EVALUATE_BUFFER

/* Rickon */
COPY_EXISTING ~BDWTR50.cre~ ~override/c#ajsq01.cre~ EVALUATE_BUFFER 
	SAY NAME1 @123 /* ~Rickon~ */
	SAY NAME2 @123 /* ~Rickon~ */
    WRITE_ASCII 0x280 ~c#ajsq01~ #32 // set DV
    WRITE_ASCII 0x2cc ~c#ajsq01~ #8 // set dialogue file
WRITE_EVALUATED_ASCII 0x248 ~c#ajsq01~ #8   // Override script
WRITE_EVALUATED_ASCII 0x250 ~BDASC2~ #8   // Class script
WRITE_EVALUATED_ASCII 0x268 ~BDSHOUT~ #8   // Default script

/* Haiva */
COPY_EXISTING ~BDWTR41.cre~ ~override/c#ajsq02.cre~ EVALUATE_BUFFER 
	SAY NAME1 @124 /* ~Haiva~ */
	SAY NAME2 @124 /* ~Haiva~ */
    WRITE_ASCII 0x280 ~c#ajsq02~ #32 // set DV
    WRITE_ASCII 0x2cc ~c#ajsq02~ #8 // set dialogue file
WRITE_EVALUATED_ASCII 0x248 ~c#ajsq02~ #8   // Override script
WRITE_EVALUATED_ASCII 0x250 ~BDASC2~ #8   // Class script
WRITE_EVALUATED_ASCII 0x268 ~BDSHOUT~ #8   // Default script

COPY ~ajantisbg1/ajantissod/item/c#ajsq01.itm~ ~override~
SAY NAME1 @125 /* ~Gauntlets of Eyes of Truth~ */
SAY NAME2 @125 /* ~Gauntlets of Eyes of Truth~ */
SAY UNIDENTIFIED_DESC @126 /* ~These Gauntlets are normal gauntlets of weapon expertise, but some admirer of Babette Maelestrom enchanted them with an additional ability to grant infravision upon its wearer.

STATISTICS:

Equipped abilities:
 THAC0: +1
 Damage: +2
 Infravision up to 120 ft.

Weight: 1~ */
SAY IDENTIFIED_DESC @126


/* transition from BG1 to SoD: in case Ajantis was in party but dead, he will be silently revived - above */

/* bddialog.2da patching - not necessary */
/* (T1) EET compatibility: LAF ~EET_NPC_TRANSITION~ - not necessary */


/* (T4) SoD bdbanter.2da - create if not present and patch with Ajantis's entry */
ACTION_IF !(FILE_EXISTS_IN_GAME ~BDBANTER.2DA~) BEGIN
  COPY ~ajantisbg1/ajantissod/2da/BDBANTER.2DA~ ~override~ 
END
ACTION_IF (FILE_EXISTS_IN_GAME ~BDBANTER.2DA~) BEGIN
  APPEND ~bdbanter.2da~
	~Ajantis 	BDAJANTB~ 
  UNLESS ~Ajantis~        
END

/* (T4) Create NPCs' Banter files if not present */
COPY ~ajantisbg1/ajantissod/2da/BDBANTER.2DA~ ~ajantisbg1/ajantissod/install~
	COUNT_2DA_COLS cols
	COUNT_2DA_ROWS cols rows
	FOR ( row = 2 ; row < rows ; row = row + 1 ) BEGIN
		READ_2DA_ENTRY %row% 0 2 npc_dv
    	READ_2DA_ENTRY %row% 1 2 banterfile
      	INNER_ACTION BEGIN
		ACTION_IF NOT FILE_EXISTS_IN_GAME ~%banterfile%.dlg~ THEN BEGIN
		<<<<<<<< .../banterfiles-inlined/%banterfile%.d
		 BEGIN %banterfile%
		>>>>>>>>
		COMPILE EVALUATE_BUFFER ~.../banterfiles-inlined/%banterfile%.d~ 
		END
      	END
	END
BUT_ONLY

/* (T4) Compile the banters in d-file */
COMPILE EVALUATE_BUFFER ~ajantisbg1/ajantissod/dialogue/banter_sod.d~



/* PIDs - last dialogue file */
COMPILE EVALUATE_BUFFER ~ajantisbg1/ajantissod/dialogue/sod_pid.d~

END //SoD



//////////////////////////////////////////////////////

/* For EE, the Ajantis' family shield BAM can be used for the family shield: if BG1NPC is installed */

BEGIN @16  /* ~Install the unique BG:EE BAM for Ajantis' Family Shield~ */

DESIGNATED 1
LABEL AjantisBG1Expansion-shieldBAM
REQUIRE_PREDICATE (GAME_IS ~bgee eet~) @17 /* ~This component is only available for the Enhanced Edition.~ */
REQUIRE_PREDICATE (FILE_EXISTS_IN_GAME ~override/x#ajshld.itm~) @18 /* ~Ajantis' shield from BG1NPC Project not installed.~ */

COPY_EXISTING ~x#ajshld.itm~ ~override~
WRITE_ASCII 0x3a ~1SHLDAJ~ #8




///////////////////////////////////////////////////////////////
/* Crossmod */
//////////////////////////////////////////////////////////////

BEGIN @90004 /* ~Ajantis SoD NPC: Crossmod Content~ */  
DESIGNATED 30 
LABEL C#Ajantis_SoD_NPC-crossmod_content 
/* make sure the component can only be installed if the main component is installed */
REQUIRE_COMPONENT ~ajantisbg1.tp2~ ~0~  @90003 /* ~Ajantis NPC for SoD component needs to be installed.~ */
REQUIRE_PREDICATE !(GAME_IS ~totsc~) @25

/* Another fine Hell */
WITH_TRA ~ajantisbg1/translations/%LANGUAGE%/crossmod_another_fine_hell.tra~ BEGIN
  INCLUDE ~ajantisbg1/ajantissod/crossmod/crossmod_another_fine_hell.tpa~
END

/* BG1NPC: shield */
ACTION_IF (FILE_EXISTS_IN_GAME ~override/x#ajshld.itm~) THEN BEGIN
PRINT @90007 /* ~BG1NPC detected.~ */
  EXTEND_BOTTOM ~BDAJANTI.bcs~ ~ajantisbg1/ajantissod/crossmod/bg1npc_shield.baf~ EVALUATE_BUFFER
END


/* The Boareskyr Bridge Scene */
ACTION_IF (MOD_IS_INSTALLED ~c#sodboabri/setup-c#sodboabri.tp2~ ~0~) THEN BEGIN
PRINT @90005 /* The Boareskyr Bridge Scene mod detected. */

// Get state for c#stff24 %after_incident_at_bridge_10% (in c#sodboabri/dialogues/bhaalheritage_boareskyr_bridge.d)
/* @76  = ~[FF Healer (female)]Everyone saw the sign of Bhaal painting itself on the ground around you. I fear what happened here will be the source of much speculation and rumors. Let us move on and leave this place.~
*/
OUTER_SET after_incident_at_bridge_10 = STATE_WHICH_SAYS 76 IN ~c#sodboabri/translations/autotra/%s/dialogues.tra~ FROM ~c#stff24~
  COMPILE EVALUATE_BUFFER ~ajantisbg1/ajantissod/crossmod/crossmod_boareskyrbridge.d~ 
     USING ~ajantisbg1/translations/%LANGUAGE%/game.TRA~
END


/* Imoen 4 Ever SoD component "Give Imoen dialogue content during chapters 8 to 12" */
ACTION_IF FILE_EXISTS_IN_GAME ~c#imback.bcs~ THEN BEGIN
PRINT @90002 /* ~Imoen 4 Ever SoD component "Give Imoen dialogue content during chapters 8 to 12" detected.~ */
  COMPILE EVALUATE_BUFFER ~ajantisbg1/ajantissod/crossmod/crossmod_i4e_sod.d~
END //I4E


