/* NPC reactions to specific crucial knowledge about Caelar's plans */

/* general dialogue timer */
IF
	InParty("Ajantis")
	CombatCounter(0)
	!See([ENEMY])
	Global("C#AjSoD_InitDialTimer","LOCALS",0)
THEN
	RESPONSE #100
		RealSetGlobalTimer("C#AjSoD_DialogReactionTimer","GLOBAL",1500)
		SetGlobal("C#AjSoD_InitDialTimer","LOCALS",1)
END	

/* 1. Hephernaan's own plans */
/* GlobalLT("bd_plot","global",485) //before the portal is opened */

/* 1.1 General reaction to Hephernaan is betraying Caelar: knowledge gained BEFORE the portal scene at Dragonspear basement; PC does NOT know that he is working for a fiend yet. */
/* This will only play with Road to Discovery installed. */
IF
/* activation */
	InParty("Ajantis")
	!RealGlobalTimerNotExpired("C#AjSoD_DialogReactionTimer","GLOBAL")
	See(Player1)
	CombatCounter(0)
	!See([ENEMY])
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	!See("bdcaelar")
	!See("bdhepher")
	Global("C#RtD_HephernaanBetrayal","GLOBAL",2)
	Global("C#RtD_HephernaanFiend","GLOBAL",0)
	GlobalLT("bd_plot","global",485) //before portal scene
	Global("C#AjSoD_HephernaanBetrayal","GLOBAL",0) 
THEN 
	RESPONSE #100
		SetGlobal("C#AjSoD_HephernaanBetrayal","GLOBAL",1)
END
/* initiation */
IF
	InParty("Ajantis")
	See(Player1)
	CombatCounter(0)
	!See([ENEMY])
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	Global("C#AjSoD_HephernaanBetrayal","GLOBAL",1)	
THEN
	RESPONSE #100
		RealSetGlobalTimer("C#AjSoD_DialogReactionTimer","GLOBAL",1500)
		StartDialogNoSet(Player1)
END

/* 1.2 General reaction to the knowledge that Hephernaan is working for a fiend */
/* this dialogue only happens if the knowledge is gained through triggers from Road to Discovery */
/* activation */
IF
	InParty("Ajantis")
	!RealGlobalTimerNotExpired("C#AjSoD_DialogReactionTimer","GLOBAL")
	See(Player1)
	CombatCounter(0)
	!See([ENEMY])
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	GlobalGT("C#RtD_HephernaanFiend","GLOBAL",1)
	GlobalLT("bd_plot","global",485) //before the portal is opened
	OR(3) //not inside castle if crusaders still hold it
		!AreaCheck("bd4300")
		GlobalLT("bd_plot","GLOBAL",315)
		GlobalGT("bd_plot","GLOBAL",380)
	OR(2) //not inside the crusader camp near entrance 
		!AreaCheck("bd5100")
		!Range("TranBD5000",35) 
	!AreaCheck("bd5200")
	OR(2) //not inside the crusader camp near entrance 
		!AreaCheck("bd5000")
		!Range("TranBD5100",35) 
	Global("C#AjSoD_HephernaanFiend","GLOBAL",0) 
THEN 
	RESPONSE #100
		SetGlobal("C#AjSoD_HephernaanFiend","GLOBAL",1)
END
/* initiation */
IF
	InParty("Ajantis")
	See(Player1)
	CombatCounter(0)
	!See([ENEMY])
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	Global("C#AjSoD_HephernaanFiend","GLOBAL",1)	
THEN
	RESPONSE #100
		RealSetGlobalTimer("C#AjSoD_DialogReactionTimer","GLOBAL",1500)
		StartDialogNoSet(Player1)
END



/* 4. Incident at Boareskyr Bridge. */

/* 4.1 Quick reaction line: will only show if The Boareskyr Bridge Scene mod is not installed / didn't run. Uses the same line! -> crossmod with TBBS is in the tp2. */
/* activation */
/* no timer here! */
IF
	InParty("Ajantis")
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	See(Player1)
	CombatCounter(0)
	!See([ENEMY])
	GlobalGT("bd_plot","global",294) //after it happened
	GlobalLT("chapter","global",10) // not if game went on
	AreaCheck("bd2000")
	!InMyArea("bdireni")
	!InMyArea("bdfist24")
	Global("C#AjSoD_c#stff24_10","GLOBAL",0) // no interjection from tbbs
	Global("C#AjSoD_BoareskyrBridgeScene","GLOBAL",0)
THEN
	RESPONSE #100
		SetGlobal("C#AjSoD_BoareskyrBridgeScene","GLOBAL",1)
END

/* initiate */
IF
	InParty("Ajantis")
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	See(Player1)
	CombatCounter(0)
	!See([ENEMY])
	Global("C#AjSoD_BoareskyrBridgeScene","GLOBAL",1)
THEN
	RESPONSE #100
		RealSetGlobalTimer("C#AjSoD_DialogReactionTimer","GLOBAL",1500) //we set timer here so there is a space to whichever next dialogue
		StartDialogNoSet(Player1)
END

/* 4.2 General dialogue after bridge incident. Will run after timer. (True also in case The Boareskyr Bridge mod is installed and the above quick reaction line didn't run.) This follow-up dialogue triggers after timer is run. */
/* activation */
IF
	InParty("Ajantis")
	!RealGlobalTimerNotExpired("C#AjSoD_DialogReactionTimer","GLOBAL")
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	See(Player1)
	CombatCounter(0)
	!See([ENEMY])
	GlobalGT("bd_plot","global",294) //after it happened
	OR(2)
		Global("C#AjSoD_BoareskyrBridgeScene","GLOBAL",2) //quick reaction line run
		Global("C#AjSoD_c#stff24_10","GLOBAL",1) // interjection from tbbs run
	GlobalLT("C#AjSoD_BoareskyrBridgeScene","GLOBAL",3)
THEN
	RESPONSE #100
		SetGlobal("C#AjSoD_BoareskyrBridgeScene","GLOBAL",3)
END
/* initiate */
IF
	InParty("Ajantis")
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	See(Player1)
	CombatCounter(0)
	!See([ENEMY])
	Global("C#AjSoD_BoareskyrBridgeScene","GLOBAL",3)
THEN
	RESPONSE #100
		RealSetGlobalTimer("C#AjSoD_DialogReactionTimer","GLOBAL",1500)
		StartDialogNoSet(Player1)
END





/* 5.4 General reaction: Caelar wants to open a portal to Avernus. */
/* activation */
IF
	InParty("Ajantis")
	!RealGlobalTimerNotExpired("C#AjSoD_DialogReactionTimer","GLOBAL")
	!See([ENEMY])
	See(Player1) 
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	CombatCounter(0)
	GlobalLT("C#RtD_KnowsPortalBlood","GLOBAL",2) //from Road to Discovery
	OR(3)
		GlobalGT("C#RtD_CaelarPlan","GLOBAL",3) //from Road to Discovery
  		GlobalGT("bd_plot","global",390) //after meeting with Caelar on Dead Man's Pass
		Global("C#RtD_RoadToDiscovery","GLOBAL",1) //check for Road to Discovery
	OR(2)
		GlobalGT("C#RtD_CaelarPlan","GLOBAL",3) //from Road to Discovery
		Global("C#RtD_RoadToDiscovery","GLOBAL",0) //broaden trigger if Road to Discovery is not installed
	GlobalLT("bd_plot","global",485) //before the portal is opened
	Global("C#AjSoD_PortalAvernus","GLOBAL",0)
THEN
	RESPONSE #100
		SetGlobal("C#AjSoD_PortalAvernus","GLOBAL",1)
END
/* initiation */
IF
	InParty("Ajantis")
	See(Player1)
	CombatCounter(0)
	!See([ENEMY])
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	Global("C#AjSoD_PortalAvernus","GLOBAL",1)	
THEN
	RESPONSE #100
		RealSetGlobalTimer("C#AjSoD_DialogReactionTimer","GLOBAL",1500)
		StartDialogNoSet(Player1)
END


/* 7. Caelar needs the PC's blood to open the portal. */

/* activation */
IF
	InParty("Ajantis")
	OR(2)
		!RealGlobalTimerNotExpired("C#AjSoD_DialogReactionTimer","GLOBAL")
		See("BDDAZZO")
	!See([ENEMY])
	See(Player1) 
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	CombatCounter(0)
	OR(2)
		GlobalGT("C#RtD_KnowsPortalBlood","GLOBAL",2) //from Road to Discovery
		Global("C#AjSoD_PortalPCBloodConclusion","GLOBAL",3)
	OR(2)
		GlobalLT("bd_plot","global",485) //before the portal is opened
		Global("X#AjantisRomanceActive","GLOBAL",2)
	Global("C#AjSoD_PortalPCBlood","GLOBAL",0)
THEN
	RESPONSE #100
		SetGlobal("C#AjSoD_PortalPCBlood","GLOBAL",1)
END
/* initiation */
IF
	InParty("Ajantis")
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	See(Player1)
	CombatCounter(0)
	!See([ENEMY])
	Global("C#AjSoD_PortalPCBlood","GLOBAL",1)	
THEN
	RESPONSE #100
		RealSetGlobalTimer("C#AjSoD_DialogReactionTimer","GLOBAL",1500)
		StartDialogNoSet(Player1)
END




/* Ajantis will react to the fact that Caelar wants the PC for "something"  (triggers with Road to Discovery) */
/* change 6.2 so it fires for all instances */
/* activation */
IF
	InParty("Ajantis")
	!RealGlobalTimerNotExpired("C#AjSoD_DialogReactionTimer","GLOBAL")
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	See(Player1)
	CombatCounter(0)
	!See([ENEMY])
	OR(2)
		GlobalGT("C#RtD_CaelarWantsPC","GLOBAL",0)
		GlobalGT("C#RtD_WantBhaalBlood","GLOBAL",0)
	GlobalLT("bd_plot","global",485) //before the portal is opened
	Global("C#AjSoD_CaelarWantsPC","GLOBAL",0)
THEN 
	RESPONSE #100
		SetGlobal("C#AjSoD_CaelarWantsPC","GLOBAL",1)
END
/* initiation */
IF
	InParty("Ajantis")
	See(Player1)
	CombatCounter(0)
	!See([ENEMY])
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	Global("C#AjSoD_CaelarWantsPC","GLOBAL",1)	
THEN
	RESPONSE #100
		RealSetGlobalTimer("C#AjSoD_DialogReactionTimer","GLOBAL",1500)
		StartDialogNoSet(Player1)
END



/* 8.2 Reaction to the knowledge that Caelar is no Bhaalchild. */
/* activation */
/* without Road to Discovery, this will only trigger if Caelar was asked about it in the Coastway Crossing parley. */
IF
	InParty("Ajantis")
	!RealGlobalTimerNotExpired("C#AjSoD_DialogReactionTimer","GLOBAL")
	See(Player1)
	CombatCounter(0)
	!See([ENEMY])
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	OR(2)
  		GlobalGT("bd_mdd018","global",0) //Caelar told she is no child of Bhaal
		GlobalGT("C#RtD_CaelarBhaalChild","GLOBAL",1) //from Road to Discovery
	GlobalLT("C#AjSoD_CaelarNoBhaalChild","GLOBAL",3) 
THEN
	RESPONSE #100
		SetGlobal("C#AjSoD_CaelarNoBhaalChild","GLOBAL",1) 
END
/* initiation */
IF
	InParty("Ajantis")
	See(Player1)
	CombatCounter(0)
	!See([ENEMY])
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	Global("C#AjSoD_CaelarNoBhaalChild","GLOBAL",1)	
THEN
	RESPONSE #100
		RealSetGlobalTimer("C#AjSoD_DialogReactionTimer","GLOBAL",1500)
		StartDialogNoSet(Player1)
END

	