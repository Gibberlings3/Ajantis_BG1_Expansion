/* Additions, to Ajantis's OVERRIDE script - main file */


/* conspiring nobles in BG city */
/* activate */
IF
	InParty(Myself)
	See(Player1)
	CombatCounter(0)
	!See([ENEMY])
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(PLAYER1,CD_STATE_NOTVALID)
	GlobalLT("C#AjantisSoD_NoblesSoD","GLOBAL",2)
	Dead("BDMAXWEL")
	Dead("BDTHERTO")
	Dead("BDARAYA")
	OR(3)
		See("BDMAXWEL")
		See("BDTHERTO")
		See("BDARAYA")
THEN
	RESPONSE #100
		IncrementGlobal("C#AjantisSoD_NoblesSoD","GLOBAL",2)
END

/* initiate */
IF
	InParty(Myself)
	See(Player1)
	CombatCounter(0)
	!See([ENEMY])
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(PLAYER1,CD_STATE_NOTVALID)
	OR(2) 
		Global("C#AjantisSoD_NoblesSoD","GLOBAL",2)
		Global("C#AjantisSoD_NoblesSoD","GLOBAL",3)	
THEN
	RESPONSE #100
		RealSetGlobalTimer("C#AjSoD_DialogReactionTimer","GLOBAL",2400)
		StartDialogNoSet(Player1)
END

/* PC opened cell after knowing it contains a doppelganger BDFFDOPP */
IF
	InParty(Myself)
	See(Player1)
	CombatCounter(0)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	AreaCheck("BD0116")
	OR(2)
		Global("BD_CELL_OPEN","BD0116",1)
		OpenState("DOOR02",TRUE)
	See("BDFFDOPP")
	Global("C#AjSoD_BDFFDOPP_11","GLOBAL",1)
THEN
	RESPONSE #100
		SetInterrupt(FALSE)
		DisplayStringHead(Myself,@117)
		SetLeavePartyDialogFile() LeaveParty() Attack("BDFFDOPP")
		SetGlobal("C#AjSoD_BDFFDOPP_11","GLOBAL",2)
		SetInterrupt(TRUE)
END
/* PC opened cell after knowing it contains a doppelganger BDFFDOP1 */
IF
	InParty(Myself)
	See(Player1)
	CombatCounter(0)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	AreaCheck("BD0116")
	OR(2)
		Global("BD_CELL_OPEN","BD0116",1)
		OpenState("DOOR02",TRUE)
	See("BDFFDOP1")
	Global("C#AjSoD_BDFFDOPP_11","GLOBAL",1)
THEN
	RESPONSE #100
		SetInterrupt(FALSE)
		DisplayStringHead(Myself,@117)
		SetLeavePartyDialogFile() LeaveParty() Attack("BDFFDOP1")
		SetGlobal("C#AjSoD_BDFFDOPP_11","GLOBAL",2)
		SetInterrupt(TRUE)
END

/* PC sided with Vampre Tsolas */

IF
	InParty(Myself)
	See(Player1)
	Global("C#AjSoD_BDISABEL_40","GLOBAL",1)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	Global("bd_123_sided_with_tsolak","global",1)
	See("bdisabel")
	See("bdtsolak")
	!Dead("bdtsolak")
THEN
	RESPONSE #100
		SetInterrupt(FALSE)
		SetLeavePartyDialogFile() LeaveParty() Enemy() Attack("bdtsolak")
		SetGlobal("C#AjSoD_BDISABEL_40","GLOBAL",2)
		SetInterrupt(TRUE)
END

/* Dwarven dig site: turns on CHARNAME if Dwarves are attacked */

IF 
	OR(4)
		Allegiance("BDDEEP",[ENEMY])
		Allegiance("BDDEEP2",[ENEMY])
		Dead("BDDEEP")
		Dead("BDDEEP2")
	OR(2)
		See("BDDEEP")
		See("BDDEEP2")
	Global("C#AjSoD_Deepvein","LOCALS",0)
THEN
	RESPONSE #100
		SetInterrupt(FALSE)
		DisplayStringHead(Myself,@118)
		SetLeavePartyDialogFile() LeaveParty() Enemy()	
		SetGlobal("C#AjSoD_Deepvein","LOCALS",1)
		SetInterrupt(TRUE)
END



/* check: PC had nightmare without Ajantis being around */

IF
	!InParty(Myself) 
	!Range(Player1,30)  //not in party, not nearby
	Global("bd_ddd","global",1) //first nightmare happened
	Global("X#AjantisRomanceActive","GLOBAL",2)
	Global("C#AjSoD_Nightmare","GLOBAL",0)
THEN
	RESPONSE #100
		SetGlobal("C#AjSoD_Nightmare","GLOBAL",2) 
END

/* PC had first nightmare: Sarevok - slayer */
IF
	InParty(Myself)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	See(Player1)
	!Detect([ENEMY])
	!Detect([EVILBUTBLUE])
	Global("bd_ddd","global",1) //first nightmare happened
	CombatCounter(0)
	Global("X#AjantisRomanceActive","GLOBAL",2)
	Global("C#AjSoD_Nightmare","GLOBAL",0)
THEN
	RESPONSE #100
		SetGlobal("C#AjSoD_Nightmare","GLOBAL",1) 
END


/* triggering. */
IF
	InParty(Myself)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	Global("C#AjSoD_Nightmare","GLOBAL",1) 
	CombatCounter(0)
THEN
	RESPONSE #100
		RealSetGlobalTimer("C#AjSoD_DialogReactionTimer","GLOBAL",1800)
		StartDialogueNoSet(Player1)
END

/* SoD first (only?) lovetalk */
/* Activation */
IF
	InParty(Myself)
	!RealGlobalTimerNotExpired("C#AjSoD_DialogReactionTimer","GLOBAL")
	See(Player1)
	CombatCounter(0)
	!See([ENEMY])
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(PLAYER1,CD_STATE_NOTVALID)
	Global("X#AjantisRomanceActive","GLOBAL",2)
	Global("C#AjantisSoD_LoveTalk","GLOBAL",0)
THEN
	RESPONSE #100
		SetGlobal("C#AjantisSoD_LoveTalk","GLOBAL",1) 
END

/* trigger */
IF
	InParty(Myself)
	See(Player1)
	CombatCounter(0)
	!See([ENEMY])
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(PLAYER1,CD_STATE_NOTVALID)
	Global("C#AjantisSoD_LoveTalk","GLOBAL",1)	
THEN
	RESPONSE #100
		RealSetGlobalTimer("C#AjSoD_DialogReactionTimer","GLOBAL",1800)
		StartDialogNoSet(Player1)
END


/* (T3) Move Ajantis to the table in the morning after the last night in the Ducal Palace (before heading out against the Crusade) */
IF
	Global("bd_plot","global",55)
	Global("C#AjSoD_bd_55","bd0103",0)
	InParty("Ajantis") 
	AreaCheck("bd0103")
THEN
	RESPONSE #100
		CutSceneId(Player1)
		ActionOverride("Ajantis",JumpToPoint([647.218]))
		ActionOverride("Ajantis",Face(S))
		SetGlobal("C#AjSoD_bd_55","bd0103",1)
END	

/* (T3) bd1000: Ajantis comments on the explosion on the bridge  */
IF
	GlobalTimerNotExpired("bd_caelar_timer","bd1000")
	AreaCheck("bd1000")
	IsValidForPartyDialog(Myself)
	Detect(Player1)	
	Global("C#AjSoD_bridge","bd1000",0)
THEN
	RESPONSE #100
		SetGlobal("C#AjSoD_bridge","bd1000",1)
		DisplayStringHead(Myself,@119)
END

/* Ajantis should not react to spike trap if he wasn't around */
IF
	!InParty(Myself)
	OR(2)
		!AreaCheck("bd7230")
		!Range("Lever",30)
	Global("C#AjSoD_UsedSpikes","GLOBAL",1) //this variable is set by patching bdlever2.BCS via tp2
	CombatCounter(0)
THEN
	RESPONSE #100
		SetGlobal("C#AjSoD_UsedSpikes","GLOBAL",3)
END

/* (T3) PC killed crusaders with the spikes */
IF
	InParty(Myself)
	IsValidForPartyDialog(Myself)
	See(Player1)
	AreaCheck("bd7230")
	Global("C#AjSoD_UsedSpikes","GLOBAL",1) //this variable is set by patching bdlever2.BCS via tp2
	CombatCounter(0)
THEN
	RESPONSE #100
		SetGlobal("C#AjSoD_UsedSpikes","GLOBAL",2)
		DisplayStringHead(Myself,@120)
END

/* PC lost Paladinhood / Rangerhood due to spiketraps */
/* Ajantis should not react if he wasn't around */
IF
	!InParty(Myself)
	!AreaCheck("bd7230")
	Global("C#AjSoD_SpikeTrapFall","GLOBAL",1) //this variable is set by patching bdlever2.BCS via tp2
	CombatCounter(0)
THEN
	RESPONSE #100
		SetGlobal("C#AjSoD_SpikeTrapFall","GLOBAL",4)
END

/* (T3) Ajantis was around: PC lost Paladinhood / Rangerhood due to spiketraps */
IF
	InParty(Myself)
	IsValidForPartyDialog(Myself)
	See(Player1)
	AreaCheck("bd7230")
	Global("C#AjSoD_SpikeTrapFall","GLOBAL",1) //this variable is set by patching bdlever2.BCS via tp2
	CombatCounter(0)
THEN
	RESPONSE #100
		SetGlobal("C#AjSoD_SpikeTrapFall","GLOBAL",2)
END
/* trigger */
IF
	See(Player1)
	CombatCounter(0)
	!See([ENEMY])
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(PLAYER1,CD_STATE_NOTVALID)
	Global("C#AjSoD_SpikeTrapFall","GLOBAL",2)
THEN
	RESPONSE #100
		StartDialogNoSet(Player1)
END


/* (T3) Ajantis reacts to Thrix's game */

/* PC offered Ajantis's soul */
IF
	InParty(Myself)
	See(Player1)
	CombatCounter(0)
	!See([ENEMY])
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(PLAYER1,CD_STATE_NOTVALID)
	OR(2) 
	/* Ajantis was chosen and the PC accepted it, but either won the game [will be addressed in dialogue] or tried to fight the demon off after losing. */
		Global("C#AjSoD_Thrix","GLOBAL",1)
	/* Ajantis was chosen before the riddle and the PC sent him off to Thrix (verbally) after losing. */
		Global("C#AjSoD_Thrix","GLOBAL",4)
	GlobalLT("bd_plot","global",550)
	OR(2)
		Global("bd_thrix_plot","global",20)
		Dead("bdthrix")  // Thrix
THEN
	RESPONSE #100
		IncrementGlobal("C#AjSoD_Thrix","GLOBAL",5) //increases by 5
END

/* trigger */
IF
	InParty(Myself)
	See(Player1)
	CombatCounter(0)
	!See([ENEMY])
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(PLAYER1,CD_STATE_NOTVALID)
	OR(2)
		Global("C#AjSoD_Thrix","GLOBAL",6)
	//  Global("C#AjSoD_Thrix","GLOBAL",7) //etc.: according to where your NPC should react and adjust the OR() accordingly
		Global("C#AjSoD_Thrix","GLOBAL",9)	
THEN
	RESPONSE #100
		StartDialogNoSet(Player1)
END

/* item reaction */
/* bdhelm05 "Battle Tankard" */

IF
	InParty(Myself) 
	!StateCheck(Player1,CD_STATE_NOTVALID)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!Detect([ENEMY])
	!Detect([EVILBUTBLUE])
	CombatCounter(0)
	HasItemEquiped("bdhelm05",Myself)
	Detect(Player1)	
	Global("C#AjSoD_SoDbdhelm05","LOCALS",0)
THEN
	RESPONSE #100
		SetGlobal("C#AjSoD_SoDbdhelm05","LOCALS",1)
		DisplayStringHead(Myself,@127)
END

/* Romance conflicts - Rasaad */
IF
	InParty(Myself)
	See(Player1)
	CombatCounter(0)
	!See([ENEMY])
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(PLAYER1,CD_STATE_NOTVALID)
	Global("C#AjSoD_IgnoreRomances","GLOBAL",0)
	!Dead("Rasaad")
	GlobalGT("bd_RasaadRomance4","GLOBAL",0) 
	Global("bd_rasaad_romanceactive","global",1)
	Global("bd_voghiln_romanceactive","global",0)
	Global("bd_corwin_romanceactive","global",0)
	OR(3)
		!RealGlobalTimerNotExpired("C#AjSoD_DialogReactionTimer","GLOBAL")
		GlobalGT("bd_plot","GLOBAL",349)
		AreaCheck("bd5100")
	Global("C#AjSoD_RomanceConflict","GLOBAL",0)
	
THEN
	RESPONSE #100
		SetGlobal("C#AjSoD_RomanceConflict","GLOBAL",1) 
END
/* Romance conflicts - Voghiln */
IF
	InParty(Myself)
	See(Player1)
	CombatCounter(0)
	!See([ENEMY])
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(PLAYER1,CD_STATE_NOTVALID)
	Global("C#AjSoD_IgnoreRomances","GLOBAL",0)
	!Dead("Voghiln")
	GlobalGT("bd_VoghilnRomance3","GLOBAL",0) 
	Global("bd_rasaad_romanceactive","global",0)
	Global("bd_voghiln_romanceactive","global",1)
	Global("bd_corwin_romanceactive","global",0)
	OR(3)
		!RealGlobalTimerNotExpired("C#AjSoD_DialogReactionTimer","GLOBAL")
		GlobalGT("bd_plot","GLOBAL",349)
		AreaCheck("bd5100")
	Global("C#AjSoD_RomanceConflict","GLOBAL",0)
	
THEN
	RESPONSE #100
		SetGlobal("C#AjSoD_RomanceConflict","GLOBAL",2) 
END
/* Romance conflicts - Corwin */
IF
	InParty(Myself)
	See(Player1)
	CombatCounter(0)
	!See([ENEMY])
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(PLAYER1,CD_STATE_NOTVALID)
	Global("C#AjSoD_IgnoreRomances","GLOBAL",0)
	!Dead("Corwin")
	GlobalGT("bd_CorwinRomance3","GLOBAL",0) 
	Global("bd_rasaad_romanceactive","global",0)
	Global("bd_voghiln_romanceactive","global",0)
	Global("bd_corwin_romanceactive","global",1)
	OR(2)
		!RealGlobalTimerNotExpired("C#AjSoD_DialogReactionTimer","GLOBAL")
		AreaCheck("bd5200")
	Global("C#AjSoD_RomanceConflict","GLOBAL",0)
	
THEN
	RESPONSE #100
		SetGlobal("C#AjSoD_RomanceConflict","GLOBAL",3) 
END

/* trigger for all in-group romance conflicts */
IF
	InParty(Myself)
	See(Player1)
	CombatCounter(0)
	!See([ENEMY])
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(PLAYER1,CD_STATE_NOTVALID)
	OR(3)
		Global("C#AjSoD_RomanceConflict","GLOBAL",1) //Rasaad
		Global("C#AjSoD_RomanceConflict","GLOBAL",2) //Voghlin
		Global("C#AjSoD_RomanceConflict","GLOBAL",3) //Corwin
THEN
	RESPONSE #100
		RealSetGlobalTimer("C#AjSoD_DialogReactionTimer","GLOBAL",600)
		StartDialogNoSet(Player1)
END

/* Kill other romances */
/* Rasaad */
IF
	InParty(Myself)
	See(Player1)
	CombatCounter(0)
	!See([ENEMY])
	Global("bd_rasaad_romanceactive","global",1)
	Global("C#AjSoD_RomanceConflict","GLOBAL",5)
	Global("C#AjSoD_KillRasaadRomance","LOCALS",0)
THEN
	RESPONSE #100
		SetGlobal("bd_rasaad_romanceactive","global",3)
		SetGlobal("C#AjSoD_KillRasaadRomance","LOCALS",1)
END
/* Voghiln */
IF
	InParty(Myself)
	See(Player1)
	CombatCounter(0)
	!See([ENEMY])
	Global("bd_voghiln_romanceactive","global",1)
	Global("C#AjSoD_RomanceConflict","GLOBAL",5)
	Global("C#AjSoD_KillVoghilnRomance","LOCALS",0)
THEN
	RESPONSE #100
		SetGlobal("bd_voghiln_romanceactive","global",3)
		SetGlobal("C#AjSoD_KillVoghilnRomance","LOCALS",1)
END
/* Corwin */
IF
	InParty(Myself)
	See(Player1)
	CombatCounter(0)
	!See([ENEMY])
	Global("bd_corwin_romanceactive","global",1)
	Global("C#AjSoD_RomanceConflict","GLOBAL",5)
	Global("C#AjSoD_KillCorwinRomance","LOCALS",0)
THEN
	RESPONSE #100
		SetGlobal("bd_corwin_romanceactive","global",3)
		SetGlobal("C#AjSoD_KillCorwinRomance","LOCALS",1)
END

/* Kill Ajantis romance */
IF
	InParty(Myself)
	See(Player1)
	CombatCounter(0)
	!See([ENEMY])
	Global("X#AjantisRomanceActive","GLOBAL",2)
	OR(3)
		Global("bd_rasaad_romanceactive","global",2)
		Global("bd_voghiln_romanceactive","global",2)
		Global("bd_corwin_romanceactive","global",2)
	GlobalLT("C#AjSoD_RomanceConflict","GLOBAL",6)
THEN
	RESPONSE #100
		SetGlobal("C#AjSoD_RomanceConflict","GLOBAL",6)
END
	

/* Dorn is in group - Ajantis leaves */
IF
	InParty(Myself)
	!RealGlobalTimerNotExpired("C#AjSoD_DialogReactionTimer","GLOBAL")
	See(Player1)
	CombatCounter(0)
	!See([ENEMY])
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(PLAYER1,CD_STATE_NOTVALID)
	InPartyAllowDead("Dorn")
	!Global("C#AjSoD_DornLeaving","GLOBAL",1)
	
THEN
	RESPONSE #100
		SetGlobal("C#AjSoD_DornLeaving","GLOBAL",1) 
END

/* trigger */
IF
	InParty(Myself)
	See(Player1)
	CombatCounter(0)
	!See([ENEMY])
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(PLAYER1,CD_STATE_NOTVALID)
	Global("C#AjSoD_DornLeaving","GLOBAL",1)
THEN
	RESPONSE #100
		RealSetGlobalTimer("C#AjSoD_DialogReactionTimer","GLOBAL",800)
		StartDialogNoSet(Player1)
END

/* Ajantis wears the Gauntlets of True Sight */
IF
	InParty(Myself)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	See(Player1)
	!Detect([ENEMY])
	HasItemEquiped("C#Ajsq01",Myself)
	CombatCounter(0)
	Global("C#AjSoD_WearsRickonsGauntlets","GLOBAL",0)
THEN
	RESPONSE #100
		SetGlobal("C#AjSoD_WearsRickonsGauntlets","GLOBAL",1) 
END
/* triggering. */
IF
	InParty(Myself)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	See(Player1)
	CombatCounter(0)
	Global("C#AjSoD_WearsRickonsGauntlets","GLOBAL",1) 
THEN
	RESPONSE #100
		RealSetGlobalTimer("C#AjSoD_DialogReactionTimer","GLOBAL",900)
		StartDialogueNoSet(Player1)
END


/* If PC stated their heritage upon marching out of BG city */
/* dialogue is in sod_important_reactions_dialogues.d */
IF
	InParty(Myself)
	!RealGlobalTimerNotExpired("C#AjSoD_DialogReactionTimer","GLOBAL")
	See(Player1)
	CombatCounter(0)
	!See([ENEMY])
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	Global("bd_not_hero","global",1)
	Global("C#AjSoD_ProclaimedBhaalheritage","GLOBAL",0)
THEN
	RESPONSE #100
		SetGlobal("C#AjSoD_ProclaimedBhaalheritage","GLOBAL",1)
END
/* trigger */
IF
	See(Player1)
	CombatCounter(0)
	!See([ENEMY])
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(PLAYER1,CD_STATE_NOTVALID)
	Global("C#AjSoD_ProclaimedBhaalheritage","GLOBAL",1)
THEN
	RESPONSE #100
		StartDialogNoSet(Player1)
END



/* How can they be so naive? */
/* Activation */
IF
	InParty(Myself)
	!RealGlobalTimerNotExpired("C#AjSoD_DialogReactionTimer","GLOBAL")
	See(Player1)
	CombatCounter(0)
	!See([ENEMY])
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(PLAYER1,CD_STATE_NOTVALID)
	Global("C#AjSoD_PortalAvernus","GLOBAL",2) //after main dialogue about the planned portal
	Global("C#AjSoD_CannonFoder","GLOBAL",0)
THEN
	RESPONSE #100
		SetGlobal("C#AjSoD_CannonFoder","GLOBAL",1) 
END

/* trigger */
IF
	InParty(Myself)
	See(Player1)
	CombatCounter(0)
	!See([ENEMY])
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(PLAYER1,CD_STATE_NOTVALID)
	Global("C#AjSoD_CannonFoder","GLOBAL",1)	
THEN
	RESPONSE #100
		RealSetGlobalTimer("C#AjSoD_DialogReactionTimer","GLOBAL",1800)
		StartDialogNoSet(Player1)
END
