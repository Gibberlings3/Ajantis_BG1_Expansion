/* Committed Romance: Korlash is defeated */

/* this scriptblock is only needed for Transitions Mod & EndlessBG1 "Korlasz' Tomb in BG1": prevent Ajantis from mentioning Korlasz if he wasn't in party when she was defeated */
IF

	!InParty("Ajantis")
	OR(2)
		GlobalGT("BD_KORLASZ_SURRENDER","GLOBAL",0)
		Dead("BDKORLAS")
	Global("C#Ajantis_BG1KorlaszDefeated","GLOBAL",0)
THEN
	RESPONSE #100
		SetGlobal("C#Ajantis_BG1KorlaszKilled","GLOBAL",2)
		SetGlobal("C#Ajantis_BG1KorlaszDefeated","GLOBAL",2)
END

IF

	InParty("Ajantis")
	!StateCheck(Player1,CD_STATE_NOTVALID)
	!StateCheck("Ajantis",CD_STATE_NOTVALID)
	See(Player1)
	!Detect([ENEMY])
	!Detect([EVILBUTBLUE])
	CombatCounter(0)
	AreaCheck("BD0130")  // 
//	%ENDOFBG1_1%  // BG1 is over, we are in SoD
	OR(2)
		Global("BD_KORLASZ_SURRENDER","GLOBAL",1)
		Dead("BDKORLAS")
	Global("C#Ajantis_BG1KorlaszTried","GLOBAL",0)
	Global("C#Ajantis_BG1KorlaszDefeated","GLOBAL",0)
	Global("X#AjantisRomanceActive","GLOBAL",2)
THEN
	RESPONSE #100
		SetGlobal("C#Ajantis_BG1KorlaszDefeated","GLOBAL",1)
END

IF

	InParty("Ajantis")
	!StateCheck(Player1,CD_STATE_NOTVALID)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	Global("C#Ajantis_BG1KorlaszDefeated","GLOBAL",1)
THEN
	RESPONSE #100
		ActionOverride("Ajantis",StartDialogNoSet(Player1))
END

/* trigger original SoD dialogue - only if Korlasz tried to surrender */
IF
	Global("C#Ajantis_BG1KorlaszTried","GLOBAL",1)
	InParty("Ajantis")
	!StateCheck(Player1,CD_STATE_NOTVALID)
	!StateCheck("Ajantis",CD_STATE_NOTVALID)
	See(Player1)
	!Detect([ENEMY])
	!Detect([EVILBUTBLUE])
	CombatCounter(0)
	Dead("bdkorlas")
	Global("C#Ajantis_BG1KorlaszKilled","GLOBAL",0)
THEN
	RESPONSE #100
		SetGlobal("C#Ajantis_BG1KorlaszKilled","GLOBAL",1)
END

IF

	InParty("Ajantis")
	!StateCheck(Player1,CD_STATE_NOTVALID)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	Global("C#Ajantis_BG1KorlaszKilled","GLOBAL",1)
THEN
	RESPONSE #100
		ActionOverride("Ajantis",StartDialogNoSet(Player1))
END
