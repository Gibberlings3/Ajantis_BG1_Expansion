<<<<<<<< .../BG1TriggerEmulation-inlined/Z!EmulAreaCheck.BAF
IF
	Delay(2)
	ActionListEmpty()
THEN
	RESPONSE #100
		SetGlobal("Z!EmulAreaCheck","GLOBAL",%Area_Number%)
		SetGlobal("Z!EmulAreaType","GLOBAL",%Area_Flags%)
		SetGlobal("Z!EmulAreaOutdoor","GLOBAL",%Variable_Outdoor%)
		SetGlobal("Z!EmulAreaCity","GLOBAL",%Variable_City%)
		SetGlobal("Z!EmulAreaForest","GLOBAL",%Variable_Forest%)
		SetGlobal("Z!EmulAreaDungeon","GLOBAL",%Variable_Dungeon%)
		SetGlobalTimer("Z!EmulAreaNotMaster","GLOBAL",4)
END
>>>>>>>>

<<<<<<<< .../BG1TriggerEmulation-inlined/Z!EmulAreaCheck1.BAF
IF
	!Global("Z!EmulAreaCheck","GLOBAL",%Area_Number%)
	!GlobalTimerNotExpired("Z!EmulAreaNotMaster","GLOBAL")
	ActionListEmpty()
THEN
	RESPONSE #100
		SetGlobal("Z!EmulAreaCheck","GLOBAL",%Area_Number%)
		SetGlobal("Z!LastMasterArea","GLOBAL",%Area_Number%)
		SetGlobal("Z!EmulAreaType","GLOBAL",%Area_Flags%)
		SetGlobal("Z!EmulAreaOutdoor","GLOBAL",%Variable_Outdoor%)
		SetGlobal("Z!EmulAreaCity","GLOBAL",%Variable_City%)
		SetGlobal("Z!EmulAreaForest","GLOBAL",%Variable_Forest%)
		SetGlobal("Z!EmulAreaDungeon","GLOBAL",%Variable_Dungeon%)
END
>>>>>>>>

COPY_EXISTING  ~AR2612.ARE~ ~override~ // two areas in BG1 have wrong flag "Outdoor"
               ~AR3317.ARE~ ~override~
  WRITE_BYTE 0x48 0
  BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING_REGEXP GLOB ~.*\.ARE~ ~override~ // for all areas in game proper script name and file
  SPRINT area_name ~%SOURCE_RES%~
  READ_ASCII 0x94 ~old_script_name~
  WRITE_EVALUATED_ASCII 0x94 ~%SOURCE_RES%~
  INNER_ACTION BEGIN
    ACTION_IF !(~%old_script_name%~ STRING_EQUAL_CASE ~%area_name%~)
          AND (FILE_EXISTS_IN_GAME ~%old_script_name%.bcs~)
      THEN BEGIN COPY_EXISTING ~%old_script_name%.bcs~ ~override/%area_name%.bcs~ END
  END
  BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING_REGEXP GLOB ~.*\.ARE~ ~override~ // adds AreaCheck emulation block to the area script
  READ_ASCII 0x94 ~Script_Name~
  PATCH_IF (~%SOURCE_RES%~ STRING_MATCHES_REGEXP ~AR[0-9][0-9][0-9][0-9]~) = 0
    THEN BEGIN READ_ASCII 0x96 ~Area_Number~ (4) END
    ELSE BEGIN SPRINT ~Area_Number~ ~0~ END
  READ_BYTE 0x48 ~Area_Flags~
  INNER_ACTION BEGIN
    ACTION_IF (NOT FILE_CONTAINS_EVALUATED(~%Script_Name%.BCS~ ~Z!EmulAreaCheck~)) // area script not already patched
      THEN BEGIN
        OUTER_SET Variable_Outdoor = ((~%Area_Flags%~ BAND 0b1) = 0b1)
        OUTER_SET Variable_City    = ((~%Area_Flags%~ BAND 0b1000) = 0b1000)
        OUTER_SET Variable_Forest  = ((~%Area_Flags%~ BAND 0b10000) = 0b10000)
        OUTER_SET Variable_Dungeon = ((~%Area_Flags%~ BAND 0b100000) = 0b100000)
        ACTION_IF (FILE_CONTAINS_EVALUATED(~MASTAREA.2DA~ ~%Script_Name%~))
          THEN BEGIN EXTEND_BOTTOM ~%Script_Name%.BCS~ ~.../BG1TriggerEmulation-inlined/Z!EmulAreaCheck1.BAF~ EVALUATE_BUFFER END //area is "master area" as in mastarea.2DA
          ELSE BEGIN EXTEND_BOTTOM ~%Script_Name%.BCS~ ~.../BG1TriggerEmulation-inlined/Z!EmulAreaCheck.BAF~  EVALUATE_BUFFER END //area is not "master area"
    END
  END
  BUT_ONLY_IF_IT_CHANGES
